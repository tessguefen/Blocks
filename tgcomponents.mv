<MIVA STANDARDOUTPUTLEVEL = "" IDENT = "$Id: tgcomponents.mv 64781 2019-05-29 21:39:05Z tguefen $">

<MvCOMMENT>
|
| Prefix			: MER-UTL-CAL
| Next Error Code	: 1
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "TGCOMPONENTS">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Components & Layouts">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "2.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.12">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Create components to easily manipulate customized layouts.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, util, component, json, clientside, provision_store, export, scheduledtask">
</MvFUNCTION>

<MvCOMMENT>
	Change Log
	1.001
		UI Updates
		- Styled Top Level Components
		- Added Collapse Toggle
	1.002
		Bug Fixes
		- Import Issue with components with no attributes.
	1.003
		UI Updates
		- Added "datetime" attribute for a Date/Time picker. Returns timestamp.
	1.004
		Added Layout Caching! :sunglasses:
	1.005
		- Fixed UI issue with Radio buttons.
	1.006
		- Added Date Cached
		- Added Date Start/ End per Layout> Components
	1.007
		- Actually Added Scheduling
		- Added Scheduled Task
	1.008
		- Updated PRV tags
		- Updated api_ver
	1.009
		- Added Check for Layout Cache for Start/End Dates per Layout on Runtime Call
		- Display Order for Components (o.m.g.)
	1.010
		- Array Field Type
	1.011
		- Added Export Option for All Layouts, or a specific layout
		- Made 'multitext' textarea larger.
	1.012
		- Max wins, column code added
	1.013
		- Minor bug fix (display order of Component Attributes)
		- Updated `Code` display on Layout Edit
		- Fixed left over data in previous modal issue (since )
	1.014
		- Major clean up / formatting
		- Added int() on certain values when inserting via lowlevel functions
	1.015
		- Security Bug Fixes Release
		- Minor XML Import Bug fix
	2.00
		- Major changes
</MvCOMMENT>

<MvCOMMENT>
|
|	data_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	sNN_TGComponents
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponents
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								image		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								alw_chldrn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponents_1 ON ' $ g.Store_Table_Prefix $ 'TGComponents ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponents_2 ON ' $ g.Store_Table_Prefix $ 'TGComponents ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGComponentAttributes
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cmpnt_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								required	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentAttributes_1 ON ' $ g.Store_Table_Prefix $ 'TGComponentAttributes ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentAttributes_2 ON ' $ g.Store_Table_Prefix $ 'TGComponentAttributes ( cmpnt_id, code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGComponentOptions
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponentOptions
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								attr_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentOptions_1 ON ' $ g.Store_Table_Prefix $ 'TGComponentOptions ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentOptions_2 ON ' $ g.Store_Table_Prefix $ 'TGComponentOptions ( attr_id, prompt )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayouts
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts
							(
								id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayouts_1 ON ' $ g.Store_Table_Prefix $ 'TGLayouts ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayouts_2 ON ' $ g.Store_Table_Prefix $ 'TGLayouts ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutComponents
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cmpnt_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								parent		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								active		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								dt_start	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
				 				dt_end		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
				 				code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_2 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( parent, disp_order )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_3 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( layout_id, code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutValues
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues
							(
								lc_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								attr_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutValues_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutValues ( lc_id, attr_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutCache
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache
							(
								layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								dt_cached	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutCache_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutCache ( layout_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Store Keys
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponents', 1 )						OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentDisplayOrder', 1 )				OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributes', 1 )				OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributeDisplayOrder', 1 )	OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptions', 1 )					OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptionDisplayOrder', 1 )		OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayouts', 1 )							OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayoutComponents', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Load_Privilege( 'TGCOMPONENTS', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'TGCOMPONENTS', 'Components & Layouts Module' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'tgcomponent', l.item ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgcomponent', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.version LT 1.004 }">
		<MvCOMMENT>
		|
		|	sNN_TGLayouts_Cache
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache
								(
									layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutCache_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutCache ( layout_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.006 }">
		<MvCOMMENT>
		|
		|	Add Timestamp for when it was last cached.
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Cache
				 				ADD
				 				(
				 					dt_cached	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ '
				 				)'}">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components
				 				ADD
				 				(
				 					dt_start	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ ',
				 					dt_end		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ '
				 				)'}">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutCache_Delete_All() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.009 }">
		<MvCOMMENT>
		|
		|	Add display_order for sNN_TGComponents
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents
				 				ADD
				 				(
				 					disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) $ '
				 				)'}">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponents_Disp_Order', 1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Update default Display Order
		|
		</MvCOMMENT>

		<MvOPENVIEW NAME	= "Merchant"
					VIEW	= "TGComponents"
					QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvWHILE EXPR = "{ NOT TGComponents.d.EOF }">
			<MvEVAL EXPR = "{ Component_Read( l.component ) }">
			<MvASSIGN NAME = "l.component:disp_order" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponents_Disp_Order' ) }">

			<MvIF EXPR = "{ NOT Component_Update( l.component ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.012 }">
		<MvCOMMENT>
		|
		|	Add column `code`
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components
				 				ADD
				 				(
				 					code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ '
				 				)'}">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvOPENVIEW NAME	= "Merchant"
					VIEW	= "TGLayouts_Components"
					QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts_Components' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.count"		VALUE = 0>
		<MvASSIGN NAME = "l.components"	VALUE = "">

		<MvWHILE EXPR = "{ NOT TGLayouts_Components.d.EOF }">
			<MvEVAL EXPR = "{ LayoutComponent_Read( l.components[ ++l.count ] ) }">
			<MvASSIGN NAME = "l.components" INDEX = "{ l.count }" MEMBER = "code" VALUE = "{ l.components[ l.count ]:id }">

			<MvSKIP NAME = "Merchant" VIEW = "TGLayouts_Components" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts_Components">

		<MvFOREACH ITERATOR = "l.component" ARRAY = "l.components">
			<MvIF EXPR = "{ NOT LayoutComponent_Update( l.component ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvFOREACH>

		<MvCOMMENT>
		|
		|	Add unique index
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components ADD UNIQUE INDEX TGLayouts_Components_3 ( layout_id, code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 2.000 }">
		<MvCOMMENT>
		|
		|	Alter sNN_TGComponents
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents MODIFY COLUMN descrip ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents CHANGE COLUMN allow_children alw_chldrn ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGComponent_Attrs
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGComponent_Attrs TO ' $ g.Store_Table_Prefix $ 'TGComponentAttributes' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes CHANGE COLUMN component_id cmpnt_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGComponent_Options
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGComponent_Options TO ' $ g.Store_Table_Prefix $ 'TGComponentOptions' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Components
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components TO ' $ g.Store_Table_Prefix $ 'TGLayoutComponents' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents CHANGE COLUMN component_id cmpnt_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN dt_start ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN dt_end ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Values
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Values TO ' $ g.Store_Table_Prefix $ 'TGLayoutValues' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues CHANGE COLUMN layouts_components_id lc_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Cache
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Cache TO ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache MODIFY COLUMN dt_cached ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Rename StoreKeys
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponents_Disp_Order',				l.TGComponents_Disp_Order )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Attrs',						l.TGComponent_Attrs )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Attrs_Disp_Order',			l.TGComponent_Attrs_Disp_Order )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Options',					l.TGComponent_Options )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Options_Disp_Order',		l.TGComponent_Options_Disp_Order )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGLayouts_Components',					l.TGLayouts_Components ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentDisplayOrder',				l.TGComponents_Disp_Order:maxvalue + 1 )		OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributes',				l.TGComponent_Attrs:maxvalue + 1 )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributeDisplayOrder',	l.TGComponent_Attrs_Disp_Order:maxvalue + 1 )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptions',					l.TGComponent_Options:maxvalue + 1 )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptionDisplayOrder',		l.TGComponent_Options_Disp_Order:maxvalue + 1 )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayoutComponents',					l.TGLayouts_Components:maxvalue + 1 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{	NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponents_Disp_Order' )		OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Attrs' )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Attrs_Disp_Order' )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Options' )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Options_Disp_Order' )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayouts_Components' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponents' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponentOptions' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponents' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentAttributes' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentAttributeDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentOptions' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentOptionDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayouts' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayoutComponents' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	util
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].LeftNavigation_Dot( l.indent - 1, 'Screen=SUTL&Store_Code=' $ encodeattribute( g.Store:code ) $ '&Module_Code=' $ encodeattribute( l.module:code ), 'Main', 'Components and Layouts' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }"> <MvFUNCTIONRETURN VALUE = 1> </MvIF>

	<MvIF EXPR = "{ g.TGCOMPONENTS_Screen }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Components', '', '' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">
		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_CSS( g.Tab ) }">

		<MvIF EXPR = "{ NOT Component_Load_ID( g.Component_ID, l.component ) }">
			ERROR: Could not load Screen for Component ID: <MvEVAL EXPR = "{ encodeentities( g.Component_ID ) }">.
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Component: ' $ encodeentities( l.component:name ), encodeentities( l.component:name ), '' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">

			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCOMPONENTS_SCREEN', 'TGCOMPONENTS_SCREEN_Components:Attributes' ) }">

			<MvHIDE FIELDS = "g.Module_Code,g.TGCOMPONENTS_Screen,g.Component_ID">

			<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_HTML( g.Tab ) }">
			<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_JavaScript( g.Tab ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Components & Layouts', '', '' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">
		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_CSS( g.Tab ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Components & Layouts', '', '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCOMPONENTS', 'TGCOMPONENTS_Layouts:Layouts,TGCOMPONENTS_Components:Components' ) }">

		<MvHIDE FIELDS = "g.Module_Code">

		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_HTML( g.Tab ) }">
		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_JavaScript( g.Tab ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_ComponentsAndLayouts_HTML" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_HTML }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<div id="tgcomponents_layouts"></div>

		<MvEVAL EXPR = "{ Element_DuplicateLayout_Dialog_HTML() }">
		<MvEVAL EXPR = "{ Element_AngularApp_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_HTML() }">
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Components' }">
		<div id="tgcomponents_components"></div>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_SCREEN_Components' }">
		<div id="tgcomponents_componentattributes"></div>
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_HTML" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_ComponentsAndLayouts_CSS" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_CSS }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_CSS() }">
		<link type="text/css" rel="stylesheet" href="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=styles.css' }">
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_CSS" VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Element_ComponentsAndLayouts_JavaScript" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_JavaScript }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_JavaScript() }">
		
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=angular.min.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=angular-ui-tree.min.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=app.js' }"></script>

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=layouts.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new Layouts_Batchlist(); } );
		</script>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Components' }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=components.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new Components_Batchlist(); } );
		</script>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_SCREEN_Components' }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=ComponentAttributes.js' }"></script>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_Set_A_Variable( 'cmpnt_id', g.Component_ID ) }">

		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new ComponentAttributes_Batchlist( cmpnt_id ); } );
		</script>
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_JavaScript" VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Element_DuplicateLayout_Dialog_HTML" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ g.Element_DuplicateLayout_Dialog_HTML }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<div id="DuplicateLayout_dialog" class="mm_dialog">
		<div id="DuplicateLayout_dialog_title" class="mm_dialog_title">Duplicate Layout</div>
		<div class="mm_clear"></div>
		<table>
			<tbody>
				<tr>
					<td class="mm_dialog_prompt_required">New Code:</td>
					<td class="DuplicateLayout_dialog_field">
						<input type="text" name="new_code" id="DuplicateLayout_New_Code">
					</td>
				</tr>
				<tr>
					<td class="mm_dialog_prompt_required">New Name:</td>
					<td class="DuplicateLayout_dialog_field">
						<input type="text" name="new_name" id="DuplicateLayout_New_Name">
					</td>
				</tr>
			</tbody>
		</table>
		<div class="mm_dialog_buttons_left">
			<input id="DuplicateLayout_dialog_button_cancel" type="button" value="Cancel">
		</div>
		<div class="mm_dialog_buttons_right"><input id="DuplicateLayout_dialog_button_save" type="button" value="Add"></div>
	</div>

	<MvASSIGN NAME = "g.Element_DuplicateLayout_Dialog_HTML" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_AngularApp_HTML" STANDARDOUTPUTLEVEL = "html, text">
	<MvINCLUDE INTERPRET = "off" FILE = "angular_app/app.html">
</MvFUNCTION>


<MvCOMMENT>
|
|	component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.function_name EQ 'layout_load_code' }">
		<MvIF EXPR = "{ l.parameter_count NE 2 }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 2 ] ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_Layout_Load_Code( l.module, l.param, ' $ l.parameters $ ')' }">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 3
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:				
	</MvASSEMBLY>
</MvFUNCTION>

<MvCOMMENT>
|
|	json
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'Components_Load_Query' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_Components_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Insert' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Update' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Delete' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Components_DisplayOrder_Update' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Components_DisplayOrder_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layouts_Load_Query' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_Layouts_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Insert' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Update' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Delete' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttributes_Load_Query' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttributes_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Insert' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Update' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Delete' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttributes_DisplayOrder_Update' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttributes_DisplayOrder_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Insert' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Update' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Delete' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Load_LayoutComponents' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Load_LayoutComponents( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Components_Load_All' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_Components_Load_All( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Save' }">								<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Save( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Duplicate' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Duplicate( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'LayoutCache_Delete' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_LayoutCache_Delete( l..module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	clientside
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ len_var( g.Filename ) }">		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	<MvELSEIF EXPR = "{ ( '.css' EIN g.Filename ) EQ len_var( g.Filename ) }">	<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/css' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'components.js' }">				<MvINCLUDE FILE = "js/components.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'layouts.js' }">				<MvINCLUDE FILE = "js/layouts.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'ComponentAttributes.js' }">	<MvINCLUDE FILE = "js/component_attrs.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'angular.min.js' }">			<MvINCLUDE FILE = "angular_app/angular.min.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'angular-ui-tree.min.js' }">	<MvINCLUDE FILE = "angular_app/angular-ui-tree.min.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'app.js' }">					<MvINCLUDE FILE = "angular_app/app.js" INTERPRET="OFF">
	<MvELSEIF EXPR = "{ g.Filename EQ 'styles.css' }">				<MvINCLUDE FILE = "angular_app/styles.css" INTERPRET="OFF">
	<MvELSE>														<MvASSIGN NAME = "l.null" VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
|	provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT><MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'layout_add' }">						<MvEVAL EXPR = "{ Module_Provision_Layout_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'layout_update'}">				<MvEVAL EXPR = "{ Module_Provision_Layout_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_add'}">				<MvEVAL EXPR = "{ Module_Provision_Component_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_update'}">				<MvEVAL EXPR = "{ Module_Provision_Component_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_delete'}">				<MvEVAL EXPR = "{ Module_Provision_Component_Delete( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_add'}">		<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_update'}">	<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_delete'}">	<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Delete( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattributeoption_add'}">	<MvEVAL EXPR = "{ Module_Provision_ComponentAttributeOption_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'layoutcomponent_add'}">			<MvEVAL EXPR = "{ Module_Provision_LayoutComponent_Add( l.module, l.child_xml ) }">
		</MvIF>
	</MvFOREACH></MvCOMMENT>
</MvFUNCTION>

<MvCOMMENT>
|
|	export
|
</MvCOMMENT>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT><MvASSIGN NAME = "g.Exp_Count" VALUE = 1>

	<MvIF EXPR = "{ NOT g.Export_Continue }">
		<MvIF EXPR = "{ fexists( '/' $ g.Export_File ) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ fdelete( '/' $ g.Export_File ) }">
		</MvIF>

		<MvASSIGN NAME = "l.wrapper" VALUE = "<Module code=\"TGCOMPONENTS\" feature=\"util\">">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_create( '/' $ g.Export_File, 'data', l.wrapper ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Current_Export EQ 'COMPONENTS' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Components Export Progress', 'EXPT' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">
		<MvASSIGN NAME = "l.count"	VALUE = "{ Export_Provisioning_Components( l.output ) }">
	<MvELSEIF EXPR = "{ g.Current_Export EQ 'LAYOUTS' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Layouts Export Progress', 'EXPT' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">
		<MvASSIGN NAME = "l.count"	VALUE = "{ Export_Provisioning_Layouts( l.output ) }">
	</MvIF>

	<MvASSIGN NAME = "l.success" VALUE = "{ file_append( '/' $ g.Export_File, 'data', l.output ) }">

	<MvIF EXPR = "{ NOT g.Export_Continue }">
		<MvASSIGN NAME = "l.wrapper" VALUE = "</Module>">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_append( '/' $ g.Export_File, 'data', l.wrapper ) }">

		<MvIF EXPR = "{ g.Export_SendEmail }">
			<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ Send_Email_Attachment( g.Export_Email, g.Export_Email, '', 'Components & Layouts Export', g.Export_File, '/', 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( '#Error#', 'Review Export Email Sent' ) }">
		</MvIF>

		<MvIF EXPR = "{ g.Current_Export EQ 'COMPONENTS' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( 'Exported Components.' ) }">
		<MvELSEIF EXPR = "{ g.Current_Export EQ 'LAYOUTS' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( 'Exported Layouts.' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( '#Error#', 'Reviews Exported to ' $ g.Export_File ) }">
	</MvIF>

	<MvHIDE FIELDS = "	g.Exp_Offset,
						g.Export_Continue,
						g.Module_Code,
						g.Exp_Count,
						g.Offset,
						g.Export_File,
						g.Export_SendEmail,
						g.Export_Email,
						g.Total_Count,
						g.Current_Export"></MvCOMMENT>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT><MvASSIGN NAME = "g.Export_File"	VALUE = "{ trim( g.Export_File ) }">
	<MvASSIGN NAME = "g.Current_Export" VALUE = "{ trim( g.Current_Export ) }">

	<MvIF EXPR = "{ len( g.Export_File ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Export_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.Export_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Export_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "g.Export_Email" 	VALUE = "{ trim( g.Export_Email ) }">

	<MvIF EXPR = "{ g.Export_SendEmail }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.Export_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Export_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.Current_Export }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Current_Export', 'Please select an export.' ) }">
	<MvELSEIF EXPR = "{ g.Current_Export NE 'COMPONENTS' AND g.Current_Export NE 'LAYOUTS' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Current_Export', 'Please select an export.' ) }">
	</MvIF></MvCOMMENT>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT><MvIF EXPR = "{ ISNULL g.Export_File }">
		<MvASSIGN NAME = "g.Export_File" VALUE = "component_layouts.xml">
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.Current_Export }">
		<MvASSIGN NAME = "g.Current_Export" VALUE = "COMPONENTS">
	</MvIF>

	<MvIF EXPR = "{ g.Export_Continue EQ 1 }">
		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export Components & Layouts to XML', 'EXPT', '' ) }">
		<MvIF EXPR = "{ g.Current_Export EQ 'COMPONENTS' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Components...', 'EXPT', 'EXPT' ) }">
		<MvELSEIF EXPR = "{ g.Current_Export EQ 'LAYOUTS' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Layouts...', 'EXPT', 'EXPT' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export Components &amp; Layouts to XML', 'EXPT', '' ) }">
		<MvDO FILE = "{ g.Module_Admin }" NAME = "l.ok" VALUE = "{ BeginContent() }">

		<div style="font-family: sans-serif;">
			<MvHIDE FIELDS = "g.Module_Code">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Export Settings', 150 ) }">
				<table cellspacing="5">
					<tr>
						<td valign="top"><strong>Export Components & Layouts to File:</strong></td>
						<td width="100%">
							<input type="text" name="Export_File" size="40" value="{ encodeentities( g.Export_File ) }"><br>
							<em>This is located in the private directory. This will be deleted if it already exists.</em>
						</td>
					</tr>
					<tr>
						<td valign="top"><strong>Export:</strong></td>
						<td width="100%">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Current_Export', 'COMPONENTS', g.Current_Export, 'Components' ) }"><br>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'Current_Export', 'LAYOUTS', g.Current_Export, '' ) }"> 
							<select name="LAYOUT_OPTION">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 0, g.LAYOUT_OPTION, 'All Layouts' ) }">
								<MvFOREACH ITERATOR = "l.layout" ARRAY = "l.layouts" COUNT = "{ Layout_Load_All( l.layouts ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.layout:id, g.LAYOUT_OPTION, l.layout:name ) }">
								</MvFOREACH>
							</select><br>
						</td>
					</tr>
					<tr>
						<td valign="top">Email File To:</td>
						<td width="100%">
							<MvIF EXPR = "{ NOT g.Export_SendEmail }">
								<label><input type="radio" name="Export_SendEmail" value="0" checked>Do Not Email<br></label>
								<input type="radio" name="Export_SendEmail" value="1">
							<MvELSE>
								<label><input type="radio" name="Export_SendEmail" value="0">Do Not Email<br></label>
								<input type="radio" name="Export_SendEmail" value="1" checked>
							</MvIF>
							<MvIF EXPR = "{ ISNULL g.Export_Email }">
								<MvASSIGN NAME = "g.Export_Email" VALUE = "{ g.Store:email }">
							</MvIF>
							<input type="text" size="40" name="Export_Email" value="{ encodeentities( g.Export_Email ) }">
						</td>
					</tr>
				</table>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }"></MvCOMMENT>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	scheduledtask
|
</MvCOMMENT>

<MvFUNCTION NAME = "ScheduledTaskModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings" VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Operations" PARAMETERS = "module var, operations var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.operations" INDEX = 1 MEMBER = "code"		VALUE = "check_layout_cache">
	<MvASSIGN NAME = "l.operations" INDEX = 1 MEMBER = "descrip"	VALUE = "Layout Cache & Scheduled Component Check">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Fields" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Validate" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Update" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Delete" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Execute" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.task:operation NE 'check_layout_cache' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', 'Unsupported operation' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Check_All() }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'W', g.Error_Code $ ': ' $ g.Error_Message ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Provision_Settings" PARAMETERS = "module var, task var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponents
|
</MvCOMMENT>

<MvFUNCTION NAME = "Component_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component:id"				VALUE = "{ TGComponents.d.id }">
	<MvASSIGN NAME = "l.component:code"				VALUE = "{ TGComponents.d.code }">
	<MvASSIGN NAME = "l.component:name"				VALUE = "{ TGComponents.d.name }">
	<MvASSIGN NAME = "l.component:descrip"			VALUE = "{ TGComponents.d.descrip }">
	<MvASSIGN NAME = "l.component:image"			VALUE = "{ TGComponents.d.image }">
	<MvASSIGN NAME = "l.component:alw_chldrn"		VALUE = "{ TGComponents.d.alw_chldrn }">
	<MvASSIGN NAME = "l.component:disp_order"		VALUE = "{ TGComponents.d.disp_order }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.component:allow_children"	VALUE = "{ TGComponents.d.alw_chldrn }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Insert" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponents' ) }">
	<MvASSIGN NAME = "l.component:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponents
						  ( id, code, name, descrip, image, alw_chldrn, disp_order )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.component:id, l.component:code, l.component:name, l.component:descrip, l.component:image, l.component:alw_chldrn, l.component:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Load_ID" PARAMETERS = "id, component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Component_Read( l.component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Load_Code" PARAMETERS = "code, component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Component_Read( l.component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Update" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponents
						  SET
							code = ?,
							name = ?,
							descrip = ?,
							image = ?,
							alw_chldrn = ?,
							disp_order = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.component:code, l.component:name, l.component:descrip, l.component:image, l.component:alw_chldrn, l.component:disp_order, l.component:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Component_Delete_LowLevel( l.id ) OR
					NOT ComponentAttributes_Delete_Component( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Components_Update_Offsets" PARAMETERS = "components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"						VALUE = 0>

	<MvASSIGN NAME = "l.state_pos"							VALUE = 1>
	<MvASSIGN NAME = "l.state_count"						VALUE = "{ miva_array_elements( l.components ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.components, l.state_count ) }">

	<MvWHILE EXPR = "{ l.state_pos LE l.state_count }">
		<MvASSIGN NAME = "g.Components_Order_ID"		VALUE = "{ l.components[ l.state_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current state at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponents',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id <> ? ORDER BY disp_order',
																				   'g.Components_Order_ID',
																				   l.components[ l.state_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponents.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.components[ l.state_pos ] }">

			<MvASSIGN NAME = "l.state_pos"	VALUE = "{ l.state_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvIF EXPR = "{ NOT Components_Update_Offsets_PastEnd( l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponents.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvCOMMENT>
			|
			| Make a hole by shifting components after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' 
									$ g.Store_Table_Prefix $ 'TGComponents 
								  SET 
									disp_order = disp_order + 1 
								  WHERE 
									disp_order>= ?' }" 
					 FIELDS	= "l.disp_order">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the state in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' 
									$ g.Store_Table_Prefix $ 'TGComponents 
								  SET 
									disp_order = ? 
								  WHERE 
									id = ?' }" 
					 FIELDS	= "l.disp_order, g.Components_Order_ID">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.state_pos"	VALUE = "{ l.state_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT Components_Update_Offsets_PastEnd( l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>		

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Components_Update_Offsets_PastEnd" PARAMETERS = "pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Components_Order_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"				VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'UPDATE ' 
								$ g.Store_Table_Prefix $ 'TGComponents 
							  SET 
								disp_order = ? 
							  WHERE 
								id = ?' }" 
				 FIELDS	= "l.disp_order, g.Components_Order_ID">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Layout_Reference" PARAMETERS = "component_id, layouts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT
								DISTINCT( lc.layout_id ) AS layout_id,
								l.code AS layout_code
							  FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents lc
							  LEFT JOIN ' $ g.Store_Table_Prefix $ 'TGLayouts l
							  ON lc.layout_id = l.id
							  WHERE
							  	component_id = ?' }"
				FIELDS	= "l.component_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layouts_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvASSIGN NAME = "l.temp:id"		VALUE = "{ TGLayoutComponents.d.layout_id }">
		<MvASSIGN NAME = "l.temp:code"		VALUE = "{ TGLayoutComponents.d.layout_code }">
		<MvASSIGN NAME = "l.layouts_count"	VALUE = "{ miva_array_insert_var( l.layouts, l.temp, -1 ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( '#Error#', l.layouts_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Components_Load_All" PARAMETERS = "components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents ORDER BY disp_order' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGComponents.d.EOF }">
		<MvEVAL EXPR = "{ Component_Read( l.components[ ++l.count ] ) }">

		<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.components[ l.count ]:id, l.components[ l.count ]:attributes ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponentAttributes
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentAttribute_Read" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_attribute:id"				VALUE = "{ TGComponentAttributes.d.id }">
	<MvASSIGN NAME = "l.component_attribute:cmpnt_id"		VALUE = "{ TGComponentAttributes.d.cmpnt_id }">
	<MvASSIGN NAME = "l.component_attribute:code"			VALUE = "{ TGComponentAttributes.d.code }">
	<MvASSIGN NAME = "l.component_attribute:prompt"			VALUE = "{ TGComponentAttributes.d.prompt }">
	<MvASSIGN NAME = "l.component_attribute:type"			VALUE = "{ TGComponentAttributes.d.type }">
	<MvASSIGN NAME = "l.component_attribute:required"		VALUE = "{ TGComponentAttributes.d.required }">
	<MvASSIGN NAME = "l.component_attribute:disp_order"		VALUE = "{ TGComponentAttributes.d.disp_order }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.component_attribute:component_id"	VALUE = "{ TGComponentAttributes.d.cmpnt_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Insert" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_attribute:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributes' ) }">
	<MvASSIGN NAME = "l.component_attribute:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
						  ( id, cmpnt_id, code, prompt, type, required, disp_order )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.component_attribute:id, l.component_attribute:cmpnt_id, l.component_attribute:code, l.component_attribute:prompt, l.component_attribute:type, l.component_attribute:required, l.component_attribute:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Load_ID" PARAMETERS = "id, component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME ="ComponentAttribute_Load_Code" PARAMETERS = "cmpnt_id, code, component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.cmpnt_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
						  SET
							code = ?,
							prompt = ?,
							type = ?,
							required = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.component_attribute:code, l.component_attribute:prompt, l.component_attribute:type, l.component_attribute:required, l.component_attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Delete" PARAMETERS = "component_attribute_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Delete_LowLevel( l.component_attribute_id ) OR
					NOT ComponentOptions_Delete_Attribute( l.component_attribute_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Delete_LowLevel" PARAMETERS = "component_attribute_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id = ?' }"
			 FIELDS	= "l.component_attribute_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttributes_Delete_Component" PARAMETERS = "cmpnt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ?' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Delete( TGComponentAttributes.d.id  ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update_Offsets" PARAMETERS = "cmpnt_id, fields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"			VALUE = 0>

	<MvASSIGN NAME = "l.field_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.field_count"			VALUE = "{ miva_array_elements( l.fields ) }">
	<MvASSIGN NAME = "g.Field_cmpnt_id"		VALUE = "{ l.cmpnt_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.fields, l.field_count ) }">

	<MvWHILE EXPR = "{ l.field_pos LE l.field_count }">
		<MvASSIGN NAME = "g.ComponentAttribute_ID" 	VALUE = "{ l.fields[ l.field_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current field at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponentAttributes',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id <> ? AND cmpnt_id = ? ORDER BY disp_order',
																				   'g.ComponentAttribute_ID, g.Field_cmpnt_id',
																				   l.fields[ l.field_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.fields[ l.field_pos ] }">

			<MvASSIGN NAME = "l.field_pos"	VALUE = "{ l.field_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets_PastEnd( l.cmpnt_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponentAttributes.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvCOMMENT>
			|
			| Make a hole by shifting fields after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentAttributes 
													SET 
														disp_order = disp_order + 1 
													WHERE 
														disp_order>= ? AND cmpnt_id = ?' }" 
													FIELDS = "l.disp_order, l.cmpnt_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the field in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentAttributes 
													SET 
														disp_order = ? 
													WHERE 
														id = ? AND cmpnt_id = ?' }" 
													FIELDS = "l.disp_order, g.ComponentAttribute_ID, l.cmpnt_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.field_pos"	VALUE = "{ l.field_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets_PastEnd( l.cmpnt_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>		

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update_Offsets_PastEnd" PARAMETERS = "cmpnt_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos" VALUE = 1>

	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.ComponentAttribute_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"				VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'TGComponentAttributes 
												SET 
													disp_order = ? 
												WHERE 
													id = ? AND cmpnt_id = ?' }" 
												FIELDS = "l.disp_order, g.ComponentAttribute_ID, l.cmpnt_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Types" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "radio,select,checkbox,text,memo,image,product,category,link,imagetype,datetime,multitext">
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponentOptions
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentOption_Read" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_option:id"			VALUE = "{ TGComponentOptions.d.id }">
	<MvASSIGN NAME = "l.component_option:attr_id"		VALUE = "{ TGComponentOptions.d.attr_id }">
	<MvASSIGN NAME = "l.component_option:prompt"		VALUE = "{ TGComponentOptions.d.prompt }">
	<MvASSIGN NAME = "l.component_option:disp_order"	VALUE = "{ TGComponentOptions.d.disp_order }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Insert" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_option:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptions' ) }">
	<MvASSIGN NAME = "l.component_option:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponentOptions
						  ( id, attr_id, prompt, disp_order )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.component_option:id, l.component_option:attr_id, l.component_option:prompt, l.component_option:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Load" PARAMETERS = "id, component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentOption_Read( l.component_option ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Update" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponentOptions SET prompt = ? WHERE id = ?' }"
			 FIELDS	= "l.component_option:prompt, l.component_option:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Delete_Attribute" PARAMETERS = "attr_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE attr_id = ?' }"
			 FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Load_Attribute" PARAMETERS = "attr_id, component_options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE attr_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGComponentOptions.d.EOF }">
		<MvEVAL EXPR = "{ ComponentOption_Read( l.component_options[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "TGComponentOptions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Update_Offsets" PARAMETERS = "attr_id, options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"				VALUE = 0>

	<MvASSIGN NAME = "l.option_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.option_count"			VALUE = "{ miva_array_elements( l.options ) }">
	<MvASSIGN NAME = "g.Option_attr_id" 		VALUE = "{ l.attr_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.options, l.option_count ) }">

	<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
		<MvASSIGN NAME = "g.Option_Order_Attribute_ID" VALUE = "{ l.options[ l.option_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current option at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponentOptions',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id <> ? AND attr_id = ? ORDER BY disp_order',
																				   'g.Option_Order_Attribute_ID, g.Option_attr_id',
																				   l.options[ l.option_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-OPTION-0009', 'Could not load display order for attribute option.' ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.options[ l.option_pos ] }">

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets_PastEnd( l.data_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponentOptions.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvCOMMENT>
			|
			| Make a hole by shifting options after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentOptions 
													SET 
														disp_order = disp_order + 1 
													WHERE 
														disp_order>= ? AND attr_id = ?' }" 
													FIELDS = "l.disp_order, l.attr_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the option in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentOptions 
													SET 
														disp_order = ? 
													WHERE 
														id = ? AND attr_id = ?' }" 
													FIELDS = "l.disp_order, g.Option_Order_Attribute_ID, l.attr_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets_PastEnd( l.data_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>		

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Update_Offsets_PastEnd" PARAMETERS = "attr_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Option_Order_Attribute_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"					VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'TGComponentOptions 
												SET 
													disp_order = ? 
												WHERE 
													id = ? AND attr_id = ?' }" 
												FIELDS = "l.disp_order, g.Option_Order_Attribute_ID, l.attr_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayouts
|
</MvCOMMENT>

<MvFUNCTION NAME = "Layout_Read" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:id"		VALUE = "{ TGLayouts.d.id }">
	<MvASSIGN NAME = "l.layout:code"	VALUE = "{ TGLayouts.d.code }">
	<MvASSIGN NAME = "l.layout:name"	VALUE = "{ TGLayouts.d.name }">
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Insert" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGLayouts' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayouts
						  ( id, code, name )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout:id, l.layout:code, l.layout:name">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_ID" PARAMETERS = "id, layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayouts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Layout_Read( l.layout ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_Code" PARAMETERS = "code, layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayouts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Layout_Read( l.layout ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Update" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Update_LowLevel( l.layout ) OR
					NOT LayoutCache_Recache( l.layout:id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_All" PARAMETERS = "layouts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayouts.d.EOF }">
		<MvEVAL EXPR = "{ Layout_Read( l.layouts[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "TGLayouts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( '#Error#', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Update_LowLevel" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayouts
						  SET
							code = ?,
							name = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.layout:code, l.layout:name, l.layout:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Delete" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Delete_LowLevel( l.layout_id )			OR
					NOT LayoutComponents_Delete_Layout( l.layout_id )	OR
					NOT LayoutCache_Delete( l.layout_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Delete_LowLevel" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE id = ?' }"
			 FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Duplicate_LowLevel" PARAMETERS = "layout_id, layout_components var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recursive Function that will duplicate the layout components.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.disp_order" VALUE = 0>

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components:children">
		<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ l.layout_id }">
		<MvASSIGN NAME = "l.layout_component:id"			VALUE = 0>
		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ int( l.layout_components:id ) }">
		<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ int( l.layout_component:component:id ) }">
		<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ ++l.disp_order }">

		<MvIF EXPR = "{ NOT LayoutComponent_Insert( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_Attributes( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Layout_Duplicate_LowLevel( l.layout_id, l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Duplicate" PARAMETERS = "layout_id, duplicate_layout_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutComponents_Load_Parent( l.duplicate_layout_id, 0, l.layout:children ) OR
					NOT Layout_Duplicate_LowLevel( l.layout_id, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutComponents
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutComponent_Read" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_component:id"			VALUE = "{ TGLayoutComponents.d.id }">
	<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ TGLayoutComponents.d.layout_id }">
	<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ TGLayoutComponents.d.cmpnt_id }">
	<MvASSIGN NAME = "l.layout_component:name"			VALUE = "{ TGLayoutComponents.d.name }">
	<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ TGLayoutComponents.d.parent }">
	<MvASSIGN NAME = "l.layout_component:active"		VALUE = "{ TGLayoutComponents.d.active }">
	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ TGLayoutComponents.d.disp_order }">
	<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = "{ TGLayoutComponents.d.dt_start }">
	<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = "{ TGLayoutComponents.d.dt_end }">
	<MvASSIGN NAME = "l.layout_component:code"			VALUE = "{ TGLayoutComponents.d.code }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.layout_component:component_id"	VALUE = "{ TGLayoutComponents.d.cmpnt_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Insert" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_component:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGLayoutComponents' ) }">

	<MvIF EXPR = "{ ISNULL l.layout_component:parent }">		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_start }">		<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_end }">		<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:disp_order }">	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
						  ( id, layout_id, cmpnt_id, name, parent, active, disp_order, dt_start, dt_end, code )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.layout_component:id, l.layout_component:layout_id, l.layout_component:cmpnt_id,
			 		   l.layout_component:name, l.layout_component:parent, l.layout_component:active,
			 		   l.layout_component:disp_order, l.layout_component:dt_start, l.layout_component:dt_end,
			 		   l.layout_component:code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_ID" PARAMETERS = "id, layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_Code" PARAMETERS = "layout_id, code, layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.layout_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Update" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.layout_component:parent }">		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_start }">		<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_end }">		<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:disp_order }">	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
					      SET
							name = ?,
							parent = ?,
							active = ?,
							disp_order = ?,
							dt_start = ?,
							dt_end = ?,
							code = ?
					      WHERE
						    id = ?' }"
			 FIELDS	= "l.layout_component:name, l.layout_component:parent, l.layout_component:active,
			 		   l.layout_component:disp_order, l.layout_component:dt_start, l.layout_component:dt_end,
			 		   l.layout_component:code, l.layout_component:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutComponent_Delete_LowLevel( l.id ) OR
					NOT LayoutValues_Delete_LayoutComponent( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Delete_Layout" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ?' }"
				FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutComponent_Delete( TGLayoutComponents.d.id  ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MVFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutValues
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutValue_Read" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_value:lc_id"					VALUE = "{ TGLayoutValues.d.lc_id  }">
	<MvASSIGN NAME = "l.layout_value:attr_id"				VALUE = "{ TGLayoutValues.d.attr_id }">
	<MvASSIGN NAME = "l.layout_value:value"					VALUE = "{ TGLayoutValues.d.value }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.layout_value:layouts_components_id"	VALUE = "{ TGLayoutValues.d.lc_id  }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Insert" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Insert_LowLevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutValue_Reference_Insert( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Insert_LowLevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutValues
						  ( lc_id, attr_id, value )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout_value:lc_id, l.layout_value:attr_id, l.layout_value:value">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Load" PARAMETERS = "lc_id, attr_id, layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutValues"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ? AND attr_id = ?' }"
				FIELDS	= "l.lc_id, l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutValues.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutValue_Read( l.layout_value ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Update" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Reference_Update( l.layout_value ) OR
					NOT LayoutValue_Update_Lowlevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Update_Lowlevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayoutValues
					      SET
							value = ?
					      WHERE
						    lc_id = ? AND
						    attr_id = ?' }"
			 FIELDS	= "l.layout_value:value, l.layout_value:lc_id, l.layout_value:attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Delete" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Reference_Delete( l.layout_value ) OR
					NOT LayoutValue_Delete_LowLevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Delete_LowLevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ? AND attr_id = ?' }"
			 FIELDS	= "l.layout_value:lc_id, l.layout_value:attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValues_Delete_LayoutComponent" PARAMETERS = "lc_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ?' }"
			 FIELDS	= "l.lc_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Insert" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Increment_RefCount( l.image:id ) }">
	<MvELSEIF EXPR = "{ GeneratedImage_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Increment_RefCount( l.image:image_id ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Update" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ LayoutValue_Load( l.layout_value:lc_id, l.layout_value:attr_id, l.prev_layout_value ) }">

	<MvIF EXPR = "{ l.prev_layout_value:value NE l.layout_value:value }">

		<MvIF EXPR = "{ NOT LayoutValue_Reference_Delete( l.prev_layout_value ) OR
						NOT LayoutValue_Reference_Insert( l.layout_value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Delete" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Decrement_RefCount( l.image:id ) }">
	<MvELSEIF EXPR = "{ GeneratedImage_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Decrement_RefCount( l.image:image_id ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Load_File" PARAMETERS = "file, generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE image = ?' }"
				FIELDS	= "l.file">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GeneratedImages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].GeneratedImage_Read( l.generatedimage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutCache
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutCache_Read" PARAMETERS = "layout_cache var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_cache:layout_id"	VALUE = "{ TGLayoutCache.d.layout_id  }">
	<MvASSIGN NAME = "l.layout_cache:value"		VALUE = "{ miva_array_deserialize( TGLayoutCache.d.value ) }">
	<MvASSIGN NAME = "l.layout_cache:dt_cached"	VALUE = "{ TGLayoutCache.d.dt_cached }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Insert" PARAMETERS = "layout_cache var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_cache:value" VALUE = "{ miva_array_serialize( l.layout_cache:value ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutCache
						  ( layout_id, value, dt_cached )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout_cache:layout_id, l.layout_cache:value, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Load" PARAMETERS = "layout_id, layout_cache var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache WHERE layout_id = ?' }"
				FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutCache_Read( l.layout_cache ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Delete" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache WHERE layout_id = ?' }"
			 FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Delete_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Check_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{
								'SELECT
									layoutcache.layout_id
								FROM
									' $ g.Store_Table_Prefix $ 'TGLayoutCache layoutcache
								JOIN
									' $ g.Store_Table_Prefix $ 'TGLayoutComponents layoutcomponents
								ON
									layoutcache.layout_id = layoutcomponents.layout_id
								WHERE
									( 
										layoutcomponents.dt_start> 0 AND
										layoutcomponents.dt_start < ? AND
										layoutcomponents.dt_start> layoutcache.dt_cached
									)
									OR
									(
										layoutcomponents.dt_end> 0 AND
										layoutcomponents.dt_end < ? AND
										layoutcomponents.dt_end> layoutcache.dt_cached
									)' }"
				FIELDS	= "s.dyn_time_t, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutCache.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutCache_Delete( TGLayoutCache.d.layout_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutCache" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Check_Layout" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{
								'SELECT
									layoutcache.layout_id
								FROM
									' $ g.Store_Table_Prefix $ 'TGLayoutCache layoutcache
								JOIN
									' $ g.Store_Table_Prefix $ 'TGLayoutComponents layoutcomponents
								ON
									layoutcache.layout_id = layoutcomponents.layout_id
								WHERE
									layoutcomponents.layout_id = ? AND
									( 
										layoutcomponents.dt_start> 0 AND
										layoutcomponents.dt_start < ? AND
										layoutcomponents.dt_start> layoutcache.dt_cached
									)
									OR
									(
										layoutcomponents.dt_end> 0 AND
										layoutcomponents.dt_end < ? AND
										layoutcomponents.dt_end> layoutcache.dt_cached
									)' }"
				FIELDS	= "l.layout_id, s.dyn_time_t, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutCache.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutCache_Delete( TGLayoutCache.d.layout_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutCache" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Recache" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:layout_id" VALUE = "{ l.layout_id }">

	<MvIF EXPR = "{ NOT LayoutCache_Delete( l.layout:layout_id )					OR
					NOT Runtime_Layout_Load( l.layout:layout_id, l.layout:value )	OR
					NOT LayoutCache_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.layout:value" VALUE = "{ miva_array_deserialize( l.layout:value ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutCache_Load" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Check if Layout Cache needs to be deleted first.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT LayoutCache_Check_Layout( l.layout_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Load( l.layout_id, l.layout ) }">
		<MvIF EXPR = "{ NOT LayoutCache_Recache( l.layout_id, l.layout ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
|
|	json functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "JSON_Components_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "g.Filter"			VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort"			VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset"			VALUE = "{ int( g.Offset ) }">
	<MvASSIGN NAME = "g.Count"			VALUE = "{ int( g.Count ) }">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGComponents', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'id:s.id,code:s.code,name:s.name,descrip:s.descrip,image:s.image,alw_chldrn:s.alw_chldrn,disp_order:s.disp_order' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'id:s.id,code:s.code,name:s.name,descrip:s.descrip,image:s.image,alw_chldrn:s.alw_chldrn,disp_order:s.disp_order', 's.disp_order' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGComponents', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGComponents.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count ) }">
					"id": <MvEVAL EXPR = "{ int( TGComponents.d.id ) }">,
					"code" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.code ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.name ) }">",
					"descrip" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.descrip ) }">",
					"image" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.image ) }">",
					"alw_chldrn" : <MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( TGComponents.d.alw_chldrn ) }">,
					"disp_order": <MvEVAL EXPR = "{ int( TGComponents.d.disp_order ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.component:code )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',		l.component:name )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Descrip',		l.component:descrip )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Image',		l.component:image )		OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'alw_chldrn',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ Component_Load_Code( l.component:code, l.existing_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Insert( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Id',			l.component:id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.component:code )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',		l.component:name )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Descrip',		l.component:descrip )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Image',		l.component:image )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'alw_chldrn',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:id, l.previous_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Id', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ ( toupper( l.previous_component:code ) NE toupper( l.component:code ) ) AND
					( Component_Load_Code( l.component:code, l.existing_component ) ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Update( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Id', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Components_DisplayOrder_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvFOREACH ITERATOR = "l.component" ARRAY = "g.Components_Order" INDEX = "l.pos">
		<MvASSIGN NAME = "l.component:id"				VALUE = "{ int( l.component:id ) }">
		<MvASSIGN NAME = "l.component:offset"			VALUE = "{ int( l.component:offset ) }">
		<MvASSIGN NAME = "l.component:original_offset"	VALUE = "{ int( l.component:original_offset ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.component:offset ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Components_Order[' $ l.pos $ ']', g.Validation_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvASSIGN NAME = "l.change_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.component" ARRAY = "g.Components_Order">
		<MvASSIGN NAME = "l.changes" INDEX = "{ ++l.change_count }"	VALUE = "{ l.component }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.change_count EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Components_Update_Offsets( l.changes ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layouts_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "g.Filter"			VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort"			VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset"			VALUE = "{ int( g.Offset ) }">
	<MvASSIGN NAME = "g.Count"			VALUE = "{ int( g.Count ) }">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGLayouts', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'id:s.id,code:s.code,name:s.name' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'id:s.id,code:s.code,name:s.name', 's.id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGLayouts', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGLayouts.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count )}">
					"id": <MvEVAL EXPR = "{ int( TGLayouts.d.id ) }">,
					"code" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGLayouts.d.code ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGLayouts.d.name ) }">"
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGLayouts" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code( 'R', 'code', l.layout:code ) OR
					NOT [ g.Module_JSON ].JSON_Input_Text( 'R', 'name', l.layout:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'id',			l.layout:id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'code',		l.layout:code )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'name',		l.layout:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_ID( l.layout:id, l.previous_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'id', 'Layout not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ ( toupper( l.previous_layout:code ) NE toupper( l.layout:code ) ) AND
					( Layout_Load_Code( l.layout:code, l.existing_layout ) ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Update( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'id', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttributes_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "g.Filter"			VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort"			VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset"			VALUE = "{ int( g.Offset ) }">
	<MvASSIGN NAME = "g.Count"			VALUE = "{ int( g.Count ) }">
	<MvASSIGN NAME = "g.cmpnt_id"		VALUE = "{ int( g.cmpnt_id ) }">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'attr.id				AS attr_id,
																				 attr.cmpnt_id			AS attr_cmpnt_id,
																				 attr.code				AS attr_code,
																				 attr.prompt			AS attr_prompt,
																				 attr.type				AS attr_type,
																				 attr.required			AS attr_required,
																				 attr.disp_order		AS attr_disp_order,
																				 opt.id					AS opt_id,
																				 opt.attr_id			AS opt_attr_id,
																				 opt.prompt				AS opt_prompt,
																				 opt.disp_order			AS opt_disp_order' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGComponentAttributes', 'attr' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'attr', g.Store_Table_Prefix $ 'TGComponentOptions', 'opt', 'opt.attr_id = attr.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'attr.cmpnt_id = ?', 'g.cmpnt_id' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'code:attr.code;opt.code,prompt:attr.prompt;opt.prompt,type:attr.type,required:attr.required' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, g.Sort,
																		'code:attr.code;opt.code,
																		 prompt:attr.prompt;opt.prompt,
																		 type:attr.type,
																		 required:attr.required',
																		'attr.disp_order;opt.disp_order' ) }">
																			
	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ l.search_sql }"
				FIELDS	= "{ l.search_fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count"	VALUE = 0>
	<MvASSIGN NAME = "l.attr_count"		VALUE = 0>
	<MvASSIGN NAME = "l.opt_count"		VALUE = 0>
	<MvASSIGN NAME = "l.last_attr_id"	VALUE = 0>
	<MvASSIGN NAME = "l.last_opt_id"	VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"data":
		[
		<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
			<MvIF EXPR = "{ l.last_attr_id NE TGComponentAttributes.d.attr_id }">
				<MvIF EXPR = "{ l.opt_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					]
				</MvIF>

				<MvIF EXPR = "{ l.attr_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.attr_count ) }">
				"id":			<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_id ) }">,
				"cmpnt_id":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_cmpnt_id ) }">,
				"code":			"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_code ) }">",
				"prompt":		"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_prompt ) }">",
				"type":			"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_type ) }">",
				"required":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_required ) }">,
				"disp_order":	<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_disp_order ) }">

				<MvASSIGN NAME = "l.total_count"	VALUE = "{ l.total_count + 1 }">
				<MvASSIGN NAME = "l.last_attr_id"	VALUE = "{ TGComponentAttributes.d.attr_id }">
				<MvASSIGN NAME = "l.last_opt_id"	VALUE = 0>
				<MvASSIGN NAME = "l.opt_count"		VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ l.last_opt_id NE TGComponentAttributes.d.opt_id AND NOT ISNULL TGComponentAttributes.d.opt_id }">
				<MvIF EXPR = "{ l.opt_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvIF>

				<MvIF EXPR = "{ NOT l.opt_count }">
					, "options":
					[
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.opt_count ) }">
				"id":			<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_id ) }">,
				"attr_id":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_attr_id ) }">,
				"prompt":		"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.opt_prompt ) }">",
				"disp_order":	<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_disp_order ) }">

				<MvASSIGN NAME = "l.total_count" VALUE = "{ l.total_count + 1 }">
				<MvASSIGN NAME = "l.last_opt_id" VALUE = "{ TGComponentAttributes.d.opt_id }">
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

		<MvIF EXPR = "{ l.opt_count }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
			]
		</MvIF>

		<MvIF EXPR = "{ l.attr_count }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
		</MvIF>
		],
		"start_offset": 0,
		"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">
	}
}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'cmpnt_id',	l.component_attribute:cmpnt_id,	-1, -1 )												OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.component_attribute:code )															OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',		l.component_attribute:prompt )															OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'R', 'Type',		l.component_attribute:type,	l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Required',	l.component_attribute:required ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component_attribute:cmpnt_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'cmpnt_id', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ ComponentAttribute_Load_Code( l.component_attribute:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Insert( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Id',			l.component_attribute:id,	-1, -1 )													OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'cmpnt_id',	l.component_attribute:cmpnt_id,	-1, -1 )												OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.component_attribute:code )															OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',		l.component_attribute:prompt )															OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'R', 'Type',		l.component_attribute:type,	l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Required',	l.component_attribute:required ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_attribute:id, l.previous_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Id', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component_attribute:cmpnt_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'cmpnt_id', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ toupper( l.component_attribute:code ) NE toupper( l.previous_component_attribute:code ) AND ComponentAttribute_Load_Code( l.component_attribute:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'code', 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Update( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Id', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttributes_DisplayOrder_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.cmpnt_id" VALUE = "{ g.cmpnt_id }">

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Components_Order" INDEX = "l.pos">
		<MvASSIGN NAME = "l.attributes_order:id"				VALUE = "{ int( l.attributes_order:id ) }">
		<MvASSIGN NAME = "l.attributes_order:offset"			VALUE = "{ int( l.attributes_order:offset ) }">
		<MvASSIGN NAME = "l.attributes_order:original_offset"	VALUE = "{ int( l.attributes_order:original_offset ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.attributes_order:offset ) }">
			<MvFUNCTIONRETURN VALUE VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Components_Order[' $ l.pos $ ']', g.Validation_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Option_Order" INDEX = "l.attr_pos">
		<MvFOREACH ITERATOR = "l.option_order" ARRAY = "l.attributes_order" INDEX = "l.option_pos">
			<MvASSIGN NAME = "l.option_order:id"				VALUE = "{ int( l.option_order:id ) }">
			<MvASSIGN NAME = "l.option_order:offset"			VALUE = "{ int( l.option_order:offset ) }">
			<MvASSIGN NAME = "l.option_order:original_offset"	VALUE = "{ int( l.option_order:original_offset ) }">

			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.option_order:offset ) }">
				<MvFUNCTIONRETURN VALUE VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Option_Order[' $ l.attr_pos $ '][ ' $ l.option_pos $ ' ]', g.Validation_Message ) }">
			</MvIF>
		</MvFOREACH>
	</MvFOREACH>

	<MvASSIGN NAME = "l.field_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Components_Order">
		<MvIF EXPR = "{ l.attributes_order:offset EQ l.attributes_order:original_offset }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.attr_changes" INDEX = "{ ++l.field_count }" VALUE = "{ l.attributes_order }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.field_count }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets( l.cmpnt_id, l.attr_changes ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Option_Order" INDEX = "l.attr_id">
		<MvASSIGN NAME = "l.option_count" VALUE = 0>

		<MvFOREACH ITERATOR = "l.option_order" ARRAY = "l.attributes_order" INDEX = "l.option_pos">
			<MvIF EXPR = "{ l.option_order:offset EQ l.option_order:original_offset }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.option_changes" INDEX = "{ ++l.option_count }" VALUE = "{ l.option_order }">
		</MvFOREACH>

		<MvIF EXPR = "{ l.option_count }">
			<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets( l.attr_id, l.option_changes ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'attr_id',	l.component_option:attr_id,	-1, -1 ) OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'prompt',	l.component_option:prompt ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_option:attr_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'attr_id', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Insert( l.component_option ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Id',		l.component_option:id,	-1, -1 )		OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'attr_id',	l.component_option:attr_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'prompt',	l.component_option:prompt ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_option:attr_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'attr_id', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Update( l.component_option ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Id', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Load_LayoutComponents" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Layout_ID', l.layout_id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_components_count" VALUE = "{ LayoutComponents_Load_Parent( l.layout_id, 0, l.layout_components ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Output( l.layout_components ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Components_Load_All" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ Components_Load_All( l.components ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Output( l.components ) }">
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = "[]">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Save" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Layout_ID', l.layout_id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ miva_json_decode( decodeattribute( g.Layout_Data ), l.layout_data ) }">

	<MvIF EXPR = "{ NOT LayoutComponents_Save( l.layout_id, l.layout_data:layout, l.layout_data:deleted ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Duplicate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Layout_ID',	l.duplicate_layout_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.layout:code )						OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',		l.layout:name )}">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Duplicate( l.layout:id, l.duplicate_layout_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_LayoutCache_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Delete_All() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvCOMMENT>
|
|	Custom functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutComponents_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ LayoutComponent_Read( l.component ) }">

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:cmpnt_id, l.component:component ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.component:cmpnt_id, l.component:component:attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{	NOT LayoutComponent_Load_Values( l.component:id, l.component:component:attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ LayoutComponent_Read( l.component ) }">

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:cmpnt_id, l.component:component ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.component:cmpnt_id, l.attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{	NOT LayoutComponent_Load_Values( l.component:id, l.attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes">
		<MvASSIGN NAME = "{ 'l.component:attributes:' $ l.attribute:code $ ':value' }" VALUE = "{ l.attribute:value }">

		<MvIF EXPR = "{ l.attribute:link }">
			<MvASSIGN NAME = "{ 'l.component:attributes:' $ l.attribute:code $ ':link' }" VALUE = "{ l.attribute:link }">
			<MvASSIGN NAME = "{ 'l.component:attributes:' $ l.attribute:code $ ':value' }" VALUE = "{ Load_URI( l.attribute:link ) }">
		<MvELSEIF EXPR = "{ l.attribute:category }">
			<MvASSIGN NAME = "{ 'l.component:attributes:' $ l.attribute:code $ ':category' }" VALUE = "{ l.attribute:category }">
		<MvELSEIF EXPR = "{ l.attribute:product }">
			<MvASSIGN NAME = "{ 'l.component:attributes:' $ l.attribute:code $ ':product' }" VALUE = "{ l.attribute:product }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttributes_Load_All" PARAMETERS = "cmpnt_id, component_attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error#' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
		<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attributes[ ++l.count ] ) }">

		<MvIF EXPR = "{ l.component_attributes[ l.count ]:type EQ 'select' OR l.component_attributes[ l.count ]:type EQ 'radio' }">
			<MvASSIGN NAME ="l.null" VALUE = "{ ComponentOptions_Load_Attribute( l.component_attributes[ l.count ]:id, l.component_attributes[ l.count ]:options ) }">
		<MvELSEIF EXPR = "{ l.component_attributes[ l.count ]:type EQ 'imagetype' }">
			<MvIF EXPR = "{ ISNULL g.Session:cache:tgcomponents:imagetypes }">
				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( g.Session:cache:tgcomponents:imagetypes )  }">
			</MvIF>

			<MvASSIGN NAME = "l.component_attributes" INDEX = "{ l.count }" MEMBER = "options" VALUE = "{ g.Session:cache:tgcomponents:imagetypes }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_Values" PARAMETERS = "lc_id, attributes var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes">
		<MvIF EXPR = "{ NOT LayoutValue_Load( l.lc_id, l.attribute:id, l.layout_value ) }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "">
			<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ISNULL l.layout_value:value }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.attribute:value" VALUE = "{ l.layout_value:value }">

		<MvIF EXPR = "{ l.attribute:type EQ 'link' }">
			<MvASSIGN NAME = "l.attribute:link" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:link:type }">		<MvASSIGN NAME = "l.attribute:link:type"		VALUE = "">	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:link:value }">		<MvASSIGN NAME = "l.attribute:link:value"		VALUE = "">	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:link:targ }">		<MvASSIGN NAME = "l.attribute:link:targ"		VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'product' }">
			<MvASSIGN NAME = "l.attribute:product" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:product:id }">		<MvASSIGN NAME = "l.attribute:product:id"		VALUE = 0>	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:product:code }">		<MvASSIGN NAME = "l.attribute:product:code"		VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'category' }">
			<MvASSIGN NAME = "l.attribute:category" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:category:id }">		<MvASSIGN NAME = "l.attribute:category:id"		VALUE = 0>	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:category:code }">	<MvASSIGN NAME = "l.attribute:category:code"	VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'multitext' }">
			<MvASSIGN NAME = "l.null" VALUE = "{ miva_splitstring( l.attribute:value, '|', l.attribute:value, 'trim' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Load_Parent_LowLevel" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND parent = ? ORDER BY disp_order' }"
				FIELDS	= "l.layout_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutComponents_Read( l.layout_components[ ++l.count ] ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( '#Error#', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Load_Parent" PARAMETERS = "layout_id, parent_id, components var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.components_count" VALUE = "{ LayoutComponents_Load_Parent_LowLevel( l.layout_id, l.parent_id, l.components ) }">

	<MvFOREACH ITERATOR = "l.component" ARRAY = "l.components" COUNT = "{ l.components_count }">
		<MvIF EXPR = "{ l.component:component:alw_chldrn EQ 1 }">
			<MvASSIGN NAME = "l.component:children_count" VALUE = "{ LayoutComponents_Load_Parent( l.component:layout_id, l.component:id, l.component:children ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.components_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save" PARAMETERS = "layout_id, layout_components var, deleted_components var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutComponents_Save_LowLevel( l.layout_id, l.layout_components )	OR
					NOT LayoutComponents_Delete_Components( l.deleted_components )			OR
					NOT LayoutCache_Recache( l.layout_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Load_Parent_LowLevel" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND parent = ? AND active = 1 ORDER BY disp_order' }"
				FIELDS	= "l.layout_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( '#Error#', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvIF EXPR = "{ Runtime_LayoutComponent_Schedule_Active( TGLayoutComponents.d.dt_start, TGLayoutComponents.d.dt_end ) }">
			<MvIF EXPR = "{ NOT Runtime_LayoutComponents_Read( l.layout_components[ ++l.count ] ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			</MvIF>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( '#Error#', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Load_Parent" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.components_count" VALUE = "{ Runtime_LayoutComponents_Load_Parent_LowLevel( l.layout_id, l.parent_id, l.layout_components ) }">

	<MvFOREACH ITERATOR = "l.component" ARRAY = "l.layout_components" COUNT = "{ l.components_count }">
		<MvIF EXPR = "{ l.component:component:alw_chldrn EQ 1 }">
			<MvASSIGN NAME = "l.component:children_count" VALUE = "{ Runtime_LayoutComponents_Load_Parent( l.layout_id, l.component:id, l.component:children ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.components_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponent_Schedule_Active" PARAMETERS = "dt_start, dt_end" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.dt_start GT 0 ) AND ( l.dt_start GT s.dyn_time_t ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ( l.dt_end GT 0 ) AND ( l.dt_end LE s.dyn_time_t ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_Layout_Load" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Load_ID( l.layout_id, l.loaded_layout ) OR
					NOT Runtime_LayoutComponents_Load_Parent( l.layout_id, 0, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save_LowLevel" PARAMETERS = "layout_id, layout_components var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recursive Function that will update the layout components.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.disp_order" VALUE = 0>

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components:children">
		<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ l.layout_id }">
		<MvASSIGN NAME = "l.layout_component:id"			VALUE = "{ int( l.layout_component:id ) }">
		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ int( l.layout_components:id ) }">
		<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ int( l.layout_component:component:id ) }">
		<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ ++l.disp_order }">

		<MvIF EXPR = "{ l.layout_component:id EQ 0 }">
			<MvIF EXPR = "{ NOT LayoutComponent_Insert( l.layout_component ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ NOT LayoutComponent_Update( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_Attributes( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_LowLevel( l.layout_id, l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save_Attributes" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.layout_component:component:attributes">
		<MvASSIGN NAME = "l.attribute:lc_id"	VALUE = "{ l.layout_component:id }">
		<MvASSIGN NAME = "l.attribute:attr_id"	VALUE = "{ l.attribute:id }">
		<MvASSIGN NAME = "l.attribute:value"	VALUE = "{ l.attribute:value }">

		<MvIF EXPR = "{ l.attribute:type EQ 'link' }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:link ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'product' }">
			<MvASSIGN NAME = "l.attribute:product:id" VALUE = 0>

			<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_Code( l.attribute:product:code, l.loaded_product ) }">
				<MvASSIGN NAME = "l.attribute:product:id" VALUE = "{ l.loaded_product:id }">
				<MvASSIGN NAME = "l.attribute:product:code" VALUE = "{ l.loaded_product:code }">
			</MvIF>

			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:product ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'category' }">
			<MvASSIGN NAME = "l.attribute:category:id" VALUE = 0>

			<MvIF EXPR = "{ [ g.Module_Library_DB ].Category_Load_Code( l.attribute:category:code, l.loaded_category ) }">
				<MvASSIGN NAME = "l.attribute:category:id" VALUE = "{ l.loaded_category:id }">
				<MvASSIGN NAME = "l.attribute:category:code" VALUE = "{ l.loaded_category:code }">
			</MvIF>

			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:category ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'multitext' }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ glosub( l.attribute:value, asciichar( 10 ), '|' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutValue_Insert( l.attribute ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT LayoutValue_Update( l.attribute ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Delete_Components" PARAMETERS = "deleted_components var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.deleted_components:children">
		<MvASSIGN NAME = "l.layout_component:id" VALUE = "{ int( l.layout_component:id ) }">

		<MvIF EXPR = "{ l.layout_component:id EQ 0 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponent_Delete( l.layout_component:id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Delete_Components( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Layout_Load_Code" PARAMETERS = "module var, param, layout_code, layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Load_Code( l.layout_code, l.loaded_layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Runtime_LayoutCache_Load( l.loaded_layout:id, l.loaded_layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.layout" VALUE = "{ l.loaded_layout:value }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_URI" PARAMETERS = "link var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.flags:nosession" VALUE = 1>

	<MvIF EXPR = "{ l.link:type EQ 'P' AND l.link:value }">		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'C' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'G' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'U' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ l.link:value }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvCOMMENT>
|
|	Helper component functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Parse_Function_Parameters" PARAMETERS = "string, function_name var, parameters var, parameter_count var" STANDARDOUTPUTLEVEL = "">	
	<MvASSIGN NAME = "l.parameter_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.loop_counter" 		VALUE = 0>
	<MvASSIGN NAME = "l.string" 			VALUE = "{ trim( l.string ) }">
	<MvASSIGN NAME = "l.starting_pos" 		VALUE = "{ indexof( '(', l.string, 1 ) + 1 }">
	<MvASSIGN NAME = "l.function_name" 		VALUE = "{ tolower( substring_var( l.string, 1, l.starting_pos - 2 ) ) }">

	<MvIF EXPR = "{ l.starting_pos EQ 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.function_name }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ substring_var( l.string, len_var( l.string ), 1 ) NE ')' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvWHILE EXPR = "{ 1 }">
		<MvASSIGN NAME = "l.quote_pos" 		VALUE = "{ indexof( '\'', l.string, l.starting_pos ) }">
		<MvASSIGN NAME = "l.end_quote_pos" 	VALUE = "{ indexof( '\'', l.string, l.quote_pos + 1 ) }">
		<MvASSIGN NAME = "l.comma_pos" 		VALUE = "{ indexof( ',', l.string, l.starting_pos ) }">

		<MvIF EXPR = "{ l.quote_pos AND NOT l.end_quote_pos }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvWHILE EXPR = "{ ( l.comma_pos LT l.end_quote_pos ) AND ( l.comma_pos GT l.quote_pos ) }">
			<MvASSIGN NAME = "l.comma_pos" VALUE = "{ indexof( ',', l.string, l.comma_pos + 1 ) }">
		</MvWHILE>

		<MvIF EXPR = "{ l.comma_pos EQ 0 }">
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ len_var( l.string ) - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ len_var( l.string ) }">
			<MvASSIGN NAME = "l.quote_pos" 		VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ l.comma_pos - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ l.comma_pos + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.param" VALUE = "{ trim( substring_var( l.string, l.start, l.end ) ) }">

		<MvIF EXPR = "{ ISNULL l.param }">
			<MvIF EXPR = "{ l.comma_pos EQ 0 AND l.parameter_count EQ 0 }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.parameters" INDEX = "{ ++l.parameter_count }" VALUE = "{ l.param }">

		<MvIF EXPR = "{ l.quote_pos EQ 0 AND l.comma_pos EQ 0 }">
			<MvWHILESTOP>
		</MvIF>

		<MvIF EXPR = "{ l.loop_counter GT 1000 }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.loop_counter" VALUE = "{ l.loop_counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Is_Variable" PARAMETERS = "variable var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.valid_chars" 		VALUE = "_.: ">
	<MvASSIGN NAME = "l.square_bracket_pos" VALUE = 0>
	<MvASSIGN NAME = "l.bracket_count" 		VALUE = 0>

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.variable ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.variable, l.pos, 1 ) }">

		<MvIF EXPR = "{ l.pos EQ 1 }">
			<MvIF EXPR = "{ ( NOT isalpha( l.char ) ) AND
							( l.char NE '_' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFORCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.char EQ '[' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count + 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos + 1 ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ ']' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count - 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos + 1 ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( NOT isdigit( l.char ) ) AND
							( NOT isalpha( l.char ) ) AND
							( NOT ( l.char IN l.valid_chars ) ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ l.bracket_count NE 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>