<MIVA STANDARDOUTPUTLEVEL = "" IDENT = "$Id: f85a76c70d32b5f6994191b8300d8082778ea378 $">

<MvCOMMENT>
|
| Prefix			: MOD-UTL-CAL
| Next Error Code	: 159
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "TGCOMPONENTS">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Components & Layouts">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "2.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.12">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Create components to easily manipulate customized layouts.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, util, component, json, clientside, provision_store, export, scheduledtask">
</MvFUNCTION>

<MvCOMMENT>
	Change Log
	1.001
		UI Updates
		- Styled Top Level Components
		- Added Collapse Toggle
	1.002
		Bug Fixes
		- Import Issue with components with no attributes.
	1.003
		UI Updates
		- Added "datetime" attribute for a Date/Time picker. Returns timestamp.
	1.004
		Added Layout Caching! :sunglasses:
	1.005
		- Fixed UI issue with Radio buttons.
	1.006
		- Added Date Cached
		- Added Date Start/ End per Layout> Components
	1.007
		- Actually Added Scheduling
		- Added Scheduled Task
	1.008
		- Updated PRV tags
		- Updated api_ver
	1.009
		- Added Check for Layout Cache for Start/End Dates per Layout on Runtime Call
		- Display Order for Components (o.m.g.)
	1.010
		- Array Field Type
	1.011
		- Added Export Option for All Layouts, or a specific layout
		- Made 'multitext' textarea larger.
	1.012
		- Max wins, column code added
	1.013
		- Minor bug fix (display order of Component Attributes)
		- Updated `Code` display on Layout Edit
		- Fixed left over data in previous modal issue (since )
	1.014
		- Major clean up / formatting
		- Added int() on certain values when inserting via lowlevel functions
	1.015
		- Security Bug Fixes Release
		- Minor XML Import Bug fix
	1.016
		- Removed decodeattribute( g.Payload )
	1.017
		- BUGFIX: 'Component_Update' > removed disp_order update.
	2.00
		- Major changes
</MvCOMMENT>

<MvCOMMENT>
|
|	data_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	sNN_TGComponents
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponents
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								descrip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								image		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								alw_chldrn	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponents_1 ON ' $ g.Store_Table_Prefix $ 'TGComponents ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponents_2 ON ' $ g.Store_Table_Prefix $ 'TGComponents ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGComponentAttributes
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cmpnt_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								type		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								required	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0004', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentAttributes_1 ON ' $ g.Store_Table_Prefix $ 'TGComponentAttributes ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentAttributes_2 ON ' $ g.Store_Table_Prefix $ 'TGComponentAttributes ( cmpnt_id, code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGComponentOptions
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGComponentOptions
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								attr_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								prompt		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$'
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentOptions_1 ON ' $ g.Store_Table_Prefix $ 'TGComponentOptions ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGComponentOptions_2 ON ' $ g.Store_Table_Prefix $ 'TGComponentOptions ( attr_id, prompt )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayouts
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts
							(
								id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayouts_1 ON ' $ g.Store_Table_Prefix $ 'TGLayouts ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayouts_2 ON ' $ g.Store_Table_Prefix $ 'TGLayouts ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutComponents
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cmpnt_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								parent		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								active		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								dt_start	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
				 				dt_end		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
				 				code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0014', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_2 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( parent, disp_order )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutComponents_3 ON ' $ g.Store_Table_Prefix $ 'TGLayoutComponents ( layout_id, code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutValues
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues
							(
								lc_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								attr_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0017', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutValues_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutValues ( lc_id, attr_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0018', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	sNN_TGLayoutCache
	|
	</MvCOMMENT>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache
							(
								layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								dt_cached	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0019', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutCache_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutCache ( layout_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0020', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Store Keys
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponents', 1 )						OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentDisplayOrder', 1 )				OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributes', 1 )				OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributeDisplayOrder', 1 )	OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptions', 1 )					OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptionDisplayOrder', 1 )		OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayouts', 1 )							OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayoutComponents', 1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Load_Privilege( 'TGCOMPONENTS', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].DomainPrivilege_Insert( 'TGCOMPONENTS', 'Components & Layouts Module' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'tgcomponent', l.item ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgcomponent', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.version LT 1.004 }">
		<MvCOMMENT>
		|
		|	sNN_TGLayouts_Cache
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache
								(
									layout_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0021', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGLayoutCache_1 ON ' $ g.Store_Table_Prefix $ 'TGLayoutCache ( layout_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0022', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.006 }">
		<MvCOMMENT>
		|
		|	Add Timestamp for when it was last cached.
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Cache ADD
				 				(
				 					dt_cached	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ '
				 				)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0023', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components ADD
				 				(
				 					dt_start	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ ',
				 					dt_end		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 ) $ '
				 				)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0024', g.MvQUERY_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutCache_Delete_All() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0025', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.009 }">
		<MvCOMMENT>
		|
		|	Add display_order for sNN_TGComponents
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents ADD
				 				(
				 					disp_order	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) $ '
				 				)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0026', g.MvQUERY_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponents_Disp_Order', 1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0027', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Update default Display Order
		|
		</MvCOMMENT>

		<MvOPENVIEW NAME	= "Merchant"
					VIEW	= "TGComponents"
					QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0028', g.MvQUERY_Error ) }">
		</MvIF>

		<MvWHILE EXPR = "{ NOT TGComponents.d.EOF }">
			<MvEVAL EXPR = "{ Component_Read( l.component ) }">
			<MvASSIGN NAME = "l.component:disp_order" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponents_Disp_Order' ) }">

			<MvCOMMENT>
			|
			|	Query directly used for backwards compatibility. v2.000 updated `Component_Update`
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponents
								  SET
									disp_order = ?
								  WHERE
									id = ?' }"
					 FIELDS	= "l.component:disp_order, l.component:id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0029', g.MvQUERY_Error ) }">
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
		</MvWHILE>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 1.012 }">
		<MvCOMMENT>
		|
		|	Add column `code`
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components ADD
				 				(
				 					code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 ) $ '
				 				)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0030', g.MvQUERY_Error ) }">
		</MvIF>

		<MvOPENVIEW NAME	= "Merchant"
					VIEW	= "TGLayouts_Components"
					QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts_Components' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0031', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.count" VALUE = 0>

		<MvWHILE EXPR = "{ NOT TGLayouts_Components.d.EOF }">
			<MvCOMMENT>
			|
			|	Query directly used for backwards compatibility. v2.000 updated `Component_Update`
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components
								  SET
									code = ?
								  WHERE
								    id = ?' }"
					 FIELDS	= "TGLayouts_Components.d.id, TGLayouts_Components.d.id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts_Components">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0032', g.MvQUERY_Error ) }">
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "TGLayouts_Components" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts_Components">

		<MvCOMMENT>
		|
		|	Add unique index
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components ADD UNIQUE INDEX TGLayouts_Components_3 ( layout_id, code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0033', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 2.000 }">
		<MvCOMMENT>
		|
		|	Alter sNN_TGComponents
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0034', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents MODIFY COLUMN descrip ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0035', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponents CHANGE COLUMN allow_children alw_chldrn ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0036', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGComponent_Attrs
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGComponent_Attrs TO ' $ g.Store_Table_Prefix $ 'TGComponentAttributes' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0037', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0038', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes CHANGE COLUMN component_id cmpnt_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0039', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGComponent_Options
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGComponent_Options TO ' $ g.Store_Table_Prefix $ 'TGComponentOptions' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0040', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0041', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Components
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Components TO ' $ g.Store_Table_Prefix $ 'TGLayoutComponents' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0042', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents CHANGE COLUMN component_id cmpnt_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0043', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN dt_start ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0044', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN dt_end ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0045', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents MODIFY COLUMN code ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0046', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Values
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Values TO ' $ g.Store_Table_Prefix $ 'TGLayoutValues' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0047', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues CHANGE COLUMN layouts_components_id lc_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0048', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Alter sNN_TGLayouts_Cache
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts_Cache TO ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0049', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache MODIFY COLUMN dt_cached ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0050', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		|	Rename StoreKeys
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponents_Disp_Order',				l.TGComponents_Disp_Order )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Attrs',						l.TGComponent_Attrs )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Attrs_Disp_Order',			l.TGComponent_Attrs_Disp_Order )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Options',					l.TGComponent_Options )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGComponent_Options_Disp_Order',		l.TGComponent_Options_Disp_Order )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Load( 'TGLayouts_Components',					l.TGLayouts_Components ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentDisplayOrder',				l.TGComponents_Disp_Order:maxvalue + 1 )		OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributes',				l.TGComponent_Attrs:maxvalue + 1 )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentAttributeDisplayOrder',	l.TGComponent_Attrs_Disp_Order:maxvalue + 1 )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptions',					l.TGComponent_Options:maxvalue + 1 )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGComponentOptionDisplayOrder',		l.TGComponent_Options_Disp_Order:maxvalue + 1 )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGLayoutComponents',					l.TGLayouts_Components:maxvalue + 1 ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{	NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponents_Disp_Order' )		OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Attrs' )				OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Attrs_Disp_Order' )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Options' )			OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponent_Options_Disp_Order' )	OR
						NOT [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayouts_Components' ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		|	Delete orphans, if any
		|
		</MvCOMMENT>

		<MvOPENVIEW NAME	= "Merchant"
					VIEW	= "TGLayoutComponents"
					QUERY	= "{ 'SELECT
								  	lc.*
								  FROM
								  	' $ g.Store_Table_Prefix $ 'TGLayoutComponents lc
								  LEFT JOIN
								  	' $ g.Store_Table_Prefix $ 'TGLayoutComponents lc2
								  ON
								  	lc.parent = lc2.id AND
								  	lc.layout_id = lc2.layout_id
								  WHERE
								  	lc2.id IS NULL AND
								  	lc.parent > 0' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0051', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.layout_components"			VALUE = "">
		<MvASSIGN NAME = "l.layout_components_count"	VALUE = 0>

		<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
			<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_components[ ++l.layout_components_count ] ) }">

			<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

		<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components" COUNT = "{ l.layout_components_count }">
			<MvASSIGN NAME = "l.null" VALUE = "{ LayoutComponent_Delete( l.layout_component:id ) }">
		</MvFOREACH>

		<MvCOMMENT>
		|
		|	Update disp_order of component layouts per layout
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ Layout_Load_All( l.layouts ) }">
			<MvFOREACH ITERATOR = "l.layout" ARRAY = "l.layouts">
				<MvASSIGN NAME = "l.disp_order"					VALUE = 0>
				<MvASSIGN NAME = "l.layout_component:children"	VALUE = "">

				<MvIF EXPR = "{ LayoutComponents_Load_Parent( l.layout:id, 0, l.layout_component:children ) }">
					<MvASSIGN NAME = "l.layout_component:layout_id"	VALUE = "{ l.layout:id }">
					<MvASSIGN NAME = "l.layout_component:code"		VALUE = "{ l.layout:code }">
					<MvASSIGN NAME = "l.layout_component:name"		VALUE = "{ l.layout:name }">
					<MvASSIGN NAME = "l.layout_component:id"		VALUE = 0>

					<MvIF EXPR = "{ NOT LayoutComponents_Save_LowLevel( l.layout:id, l.layout_component, l.disp_order ) }">
						<MvFUNCTIONRETURN VALUE = 0>
					</MvIF>
				</MvIF>
			</MvFOREACH>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponents' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGComponentOptions' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayouts' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutValues' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponents' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentAttributes' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentAttributeDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentOptions' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGComponentOptionDisplayOrder' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayouts' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGLayoutComponents' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	util
|
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Action_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen_Privileges" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = -1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, indent" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].LeftNavigation_Dot( l.indent - 1, 'Screen=SUTL&Store_Code=' $ encodeattribute( g.Store:code ) $ '&Module_Code=' $ encodeattribute( l.module:code ), 'Main', 'Components and Layouts' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ ISNULL g.Admin_Open_Store }"> <MvFUNCTIONRETURN VALUE = 1> </MvIF>

	<MvIF EXPR = "{ g.TGCOMPONENTS_Screen }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Components', '', '' ) }">

		<MvIF EXPR = "{ NOT Component_Load_ID( g.Component_ID, l.component ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0052', 'Component not found' ) }">
			</MvIF>
			<MvFUNCTIONRETURN VALUE = 0>
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Component: ' $ encodeentities( l.component:name ), encodeentities( l.component:name ), '' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">

			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCOMPONENTS_SCREEN', 'TGCOMPONENTS_SCREEN_Components:Attributes' ) }">

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">
			<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_CSS( g.Tab ) }">

			<MvHIDE FIELDS = "g.Module_Code,g.TGCOMPONENTS_Screen,g.Component_ID">

			<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_HTML( g.Tab ) }">
			<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_JavaScript( g.Tab ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Components & Layouts', '', '' ) }">


		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Components & Layouts', '', '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawTabs( 'TGCOMPONENTS', 'TGCOMPONENTS_Layouts:Layouts,TGCOMPONENTS_Components:Components' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_AdminUI_CSS() }">
		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_CSS( g.Tab ) }">

		<MvHIDE FIELDS = "g.Module_Code">


		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_HTML( g.Tab ) }">
		<MvEVAL EXPR = "{ Element_ComponentsAndLayouts_JavaScript( g.Tab ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons( '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_ComponentsAndLayouts_HTML" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_HTML }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<div id="tgcomponents_layouts"></div>

		<MvEVAL EXPR = "{ Element_DuplicateLayout_Dialog_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_HTML() }">

		<MvEVAL EXPR = "{ Element_AngularApp_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_HTML() }">
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Components' }">
		<div id="tgcomponents_components"></div>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_SCREEN_Components' }">
		<div id="tgcomponents_componentattributes"></div>
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_HTML" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_ComponentsAndLayouts_CSS" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_CSS }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_CSS() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Privileges_JavaScript() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_CSS() }">
		<link type="text/css" rel="stylesheet" href="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=styles.css' }">
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_CSS" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_ComponentsAndLayouts_JavaScript" PARAMETERS = "tab" STANDARDOUTPUTLEVEL = "text, html">
	<MvIF EXPR = "{ g.Element_ComponentsAndLayouts_JavaScript }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">

	<MvIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Layouts' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CategoryLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].Element_PageLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ActionDialog_JavaScript() }">

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=angular.min.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=angular-ui-tree.min.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=app.js' }"></script>

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=duplicatelayoutdialog.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=layoutdialog.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=layoutlist.js' }"></script>

		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new TGLayoutList(); } );
		</script>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_Components' }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=componentlist.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new TGComponentList(); } );
		</script>
	<MvELSEIF EXPR = "{ l.tab EQ 'TGCOMPONENTS_SCREEN_Components' }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=TGCOMPONENTS&Filename=componentattributelist.js' }"></script>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_Set_A_Variable( 'cmpnt_id', g.Component_ID ) }">

		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new ComponentAttributes_List( cmpnt_id ); } );
		</script>
	</MvIF>

	<MvASSIGN NAME = "g.Element_ComponentsAndLayouts_JavaScript" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_DuplicateLayout_Dialog_HTML" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ g.Element_DuplicateLayout_Dialog_HTML }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<div id="DuplicateLayout_dialog" class="mm_dialog">
		<div id="DuplicateLayout_dialog_title" class="mm_dialog_title">Duplicate Layout</div>
		<div class="mm_clear"></div>
		<table>
			<tbody>
				<tr>
					<td class="mm_dialog_prompt_required">New Code:</td>
					<td class="DuplicateLayout_dialog_field">
						<input type="text" name="new_code" id="DuplicateLayout_New_Code">
					</td>
				</tr>
				<tr>
					<td class="mm_dialog_prompt_required">New Name:</td>
					<td class="DuplicateLayout_dialog_field">
						<input type="text" name="new_name" id="DuplicateLayout_New_Name">
					</td>
				</tr>
			</tbody>
		</table>
		<div class="mm_dialog_buttons_left">
			<input id="DuplicateLayout_dialog_button_cancel" type="button" value="Cancel">
		</div>
		<div class="mm_dialog_buttons_right"><input id="DuplicateLayout_dialog_button_save" type="button" value="Add"></div>
	</div>

	<MvASSIGN NAME = "g.Element_DuplicateLayout_Dialog_HTML" VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Element_AngularApp_HTML" STANDARDOUTPUTLEVEL  = "text, html, compresswhitespace">
	<div id="ComponentsController_ID" ng-controller="ComponentsController" ng-app="Components" ng-mouseup="toggleNodeActions();">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_Begin( 'layout_dialog' ) }">
		<MvINCLUDE FILE = "angular_app/app-dialog.mv" INTERPRET="OFF">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_End( 'layout_dialog' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_Begin( 'layoutcomponent' ) }">
		<MvINCLUDE FILE = "angular_app/app-component-dialog.mv" INTERPRET="OFF">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_End( 'layoutcomponent' ) }">
		<MvINCLUDE FILE = "angular_app/app.html" INTERPRET="OFF">
	</div>
</MvFUNCTION>

<MvCOMMENT>
|
|	component
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.function_name EQ 'layout_load_code' }">
		<MvIF EXPR = "{ l.parameter_count NE 2 }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 2 ] ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_Layout_Load_Code( l.module, l.param, ' $ l.parameters $ ')' }">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 3
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>
</MvFUNCTION>

<MvCOMMENT>
|
|	json
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'ComponentList_Load_Query' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentList_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Insert' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Update' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Component_Delete' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Component_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Components_DisplayOrder_Update' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_Components_DisplayOrder_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'LayoutList_Load_Query' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_LayoutList_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Insert' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Update' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Delete' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttributes_Load_Query' }">			<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttributes_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Insert' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Update' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttribute_Delete' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttribute_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentAttributes_DisplayOrder_Update' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentAttributes_DisplayOrder_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Insert' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Update' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentOption_Delete' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentOption_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'LayoutComponents_Load_Layout' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_LayoutComponents_Load_Layout( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'ComponentList_Load_All' }">					<MvFUNCTIONRETURN VALUE = "{ JSON_ComponentList_Load_All( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Save' }">								<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Save( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Layout_Duplicate' }">							<MvFUNCTIONRETURN VALUE = "{ JSON_Layout_Duplicate( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'LayoutCache_Delete' }">						<MvFUNCTIONRETURN VALUE = "{ JSON_LayoutCache_Delete( l..module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	clientside
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ len_var( g.Filename ) }">		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	<MvELSEIF EXPR = "{ ( '.css' EIN g.Filename ) EQ len_var( g.Filename ) }">	<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/css' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Filename EQ 'functions.js' }">					<MvINCLUDE INTERPRET = "off" FILE = "js/functions.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'componentlist.js' }">			<MvINCLUDE INTERPRET = "off" FILE = "js/componentlist.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'layoutlist.js' }">				<MvINCLUDE INTERPRET = "off" FILE = "js/layoutlist.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'duplicatelayoutdialog.js' }">	<MvINCLUDE INTERPRET = "off" FILE = "js/duplicatelayoutdialog.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'layoutdialog.js' }">				<MvINCLUDE INTERPRET = "off" FILE = "js/layoutdialog.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'componentattributelist.js' }">	<MvINCLUDE INTERPRET = "off" FILE = "js/componentattributelist.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'angular.min.js' }">				<MvINCLUDE INTERPRET = "off" FILE = "angular_app/angular.min.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'angular-ui-tree.min.js' }">		<MvINCLUDE INTERPRET = "off" FILE = "angular_app/angular-ui-tree.min.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'app.js' }">						<MvINCLUDE INTERPRET = "off" FILE = "angular_app/app.js">
	<MvELSEIF EXPR = "{ g.Filename EQ 'styles.css' }">					<MvINCLUDE INTERPRET = "off" FILE = "angular_app/styles.css">
	<MvELSE>															<MvASSIGN NAME = "l.null" VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
|	provision_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'layout_add' }">							<MvEVAL EXPR = "{ Module_Provision_Layout_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'layout_update' }">					<MvEVAL EXPR = "{ Module_Provision_Layout_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'layout_delete' }">					<MvEVAL EXPR = "{ Module_Provision_Layout_Delete( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_add' }">					<MvEVAL EXPR = "{ Module_Provision_Component_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_update' }">				<MvEVAL EXPR = "{ Module_Provision_Component_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'component_delete' }">				<MvEVAL EXPR = "{ Module_Provision_Component_Delete( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_add' }">			<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_update' }">		<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Update( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattribute_delete' }">		<MvEVAL EXPR = "{ Module_Provision_ComponentAttribute_Delete( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'componentattributeoption_add' }">	<MvEVAL EXPR = "{ Module_Provision_ComponentAttributeOption_Add( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'layoutcomponent_add' }">				<MvEVAL EXPR = "{ Module_Provision_LayoutComponent_Add( l.module, l.child_xml ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
|
|	Provisioning Helper Func
|
</MvCOMMENT>
<MvFUNCTION NAME = "Module_Provision_Layout_Add" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Layout_Add>
			<Code>Your_Code_Here</Code>
			<Name>Your Name Here</Name>
		</Layout_Add>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 'R', l.provide_xml, 'Code', l.layout:code ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml, 'Name', l.layout:name ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Layout_Update" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Layout_Update code="Your_Code_Here">
			<Code>Your_New_Code_Here</Code>
			<Name>Your New Name Here</Name>
		</Layout_Update>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_Code( l.code, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A layout with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 'O', l.provide_xml, 'Code', l.layout:code ) OR 
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'O', l.provide_xml, 'Name', l.layout:name ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ toupper( l.layout:code ) NE toupper( l.code ) }">
		<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Update( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Layout_Delete" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Layout_Delete code="Your_Code_Here" />
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_Code( l.code, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A layout with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Delete( l.layout:id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Component_Add" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Component_Add>
			<Code>Your_Code_Here</Code>
			<Name>Your Name Here</Name>
			<Descrip>Your Description</Descrip>
			<Image>graphics/00000001/leia.png</Image>
			<Allow_Children>1</Allow_Children>
		</Component_Add>
	</MvCOMMENT>

	<MvASSIGN NAME = "l.component:alw_chldrn" VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 		'R', l.provide_xml, 'Code',				l.component:code )		OR 
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'R', l.provide_xml, 'Name',				l.component:name )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'O', l.provide_xml, 'Descrip',			l.component:descrip )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml, 'Image',			l.component:image )		OR 
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'Allow_Children',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ Component_Load_Code( l.component:code, l.existing_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Insert( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Component_Update" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Component_Update code="My_Old_Code_Here">
			<Code>Your_Code_Here</Code>
			<Name>Your Name Here</Name>
			<Descrip>Your Description</Descrip>
			<Image>graphics/00000001/leia.png</Image>
			<Allow_Children>1</Allow_Children>
		</Component_Update>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 		'O', l.provide_xml, 'Code',				l.component:code )		OR 
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'O', l.provide_xml, 'Name',				l.component:name )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'O', l.provide_xml, 'Descrip',			l.component:descrip )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O', l.provide_xml, 'Image',			l.component:image )		OR 
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O', l.provide_xml, 'Allow_Children',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ toupper( l.component:code ) NE toupper( l.code ) }">
		<MvIF EXPR = "{ Component_Load_Code( l.component:code, l.existing_component ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Update( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Component_Delete" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Component_Delete code="My_Code_Here">
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'code', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A component with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Delete( l.component:id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_ComponentAttribute_Add" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<ComponentAttribute_Add component="My_Component">
			<Code>MyAttribute</Code>
			<Prompt>My Prompt</Prompt>
			<Type>image</Type>
			<Required>1</Required>
		</ComponentAttribute_Add>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'component', l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A component with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.component_attribute:cmpnt_id" VALUE = "{ l.component:id }">
	<MvASSIGN NAME = "l.component_attribute:required" VALUE = 0>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 		'R', l.provide_xml,	'Code',		l.component_attribute:code )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'R', l.provide_xml,	'Prompt',	l.component_attribute:prompt )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 		'R', l.provide_xml,	'Type',		l.component_attribute:type, l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'O', l.provide_xml,	'Required',	l.component_attribute:required ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ ComponentAttribute_Load_Code( l.component_attribute:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Insert( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_ComponentAttribute_Update" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<ComponentAttribute_Update component="My_Component" code="Attribute_Code">
			<Code>MyAttribute</Code>
			<Prompt>My Prompt</Prompt>
			<Type>image</Type>
			<Required>1</Required>
		</ComponentAttribute_Update>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'component',	l.component_code ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'code',			l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.component_code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component with the code \'' $ l.component_code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_Code( l.component:id, l.code, l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component Attribute with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 		'O', l.provide_xml,	'Code',		l.component_attribute:code )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 		'O', l.provide_xml,	'Prompt',	l.component_attribute:prompt )															OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 		'O', l.provide_xml,	'Type',		l.component_attribute:type, l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean( 	'O', l.provide_xml,	'Required',	l.component_attribute:required ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ toupper( l.component_attribute:code ) NE toupper( l.code ) }">
		<MvIF EXPR = "{ ComponentAttribute_Load_Code( l.component:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Update( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_ComponentAttribute_Delete" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<ComponentAttribute_Delete component="My_Component" code="Attribute_Code" />
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'component',	l.component_code ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'code',			l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.component_code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component with the code \'' $ l.component_code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_Code( l.component:id, l.code, l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component Attribute with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Delete( l.component_attribute:id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_ComponentAttributeOption_Add" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<ComponentAttributeOption_Add component="My_Component" code="Attribute_Code">
			<Prompt>My Prompt</Prompt>
		</ComponentAttributeOption_Add>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'component',	l.component_code ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'code',			l.code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.component_code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component with the code \'' $ l.component_code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_Code( l.component:id, l.code, l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component Attribute with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ ( l.component_attribute:type NE 'radio' ) AND
					( l.component_attribute:type NE 'select') }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Attributes must be radio or select to have options.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml, 'Prompt', l.component_option:prompt ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.component_option:attr_id" VALUE = "{ l.component_attribute:id }">

	<MvIF EXPR = "{ NOT ComponentOption_Insert( l.component_option ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_LayoutComponent_Add" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<LayoutComponent_Add layout="My_Layout_Code" component="Component_Code">
			<Name>Name</Name>
			<Code>MyCode</Code>
			<Active>1</Active>
			<Parent>Parent_Component_Code</Parent>
			<Date_Start>
				<Day>01</Day>
				<Month>02</Month>
				<Year>2019</Year>
				<Hour>11</Hour>
				<Minute>30</Minute>
				<Second>00</Second>
			</Date_Start>
			<Date_End>
				<Day>01</Day>
				<Month>02</Month>
				<Year>2019</Year>
				<Hour>12</Hour>
				<Minute>00</Minute>
				<Second>00</Second>
			</Date_End>
			<Attributes>
				<Attribute code="MyAttribute">
					<Value>My Value Here</Value>
				</Attribute>
				<Attribute code="MyAttributeLink">
					<LinkType>Product</LinkType>
					<Value>My Value Here</Value>
				</Attribute>
				<Attribute code="Some_DateTime">
					<Value>
						<Day>01</Day>
						<Month>02</Month>
						<Year>2019</Year>
						<Hour>11</Hour>
						<Minute>30</Minute>
						<Second>00</Second>
					</Value>
				</Attribute>
			</Attributes>
		</LayoutComponent_Add>
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'layout',		l.layout_code ) OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml,	'component',	l.component_code ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_Code( l.layout_code, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'A layout with the code \'' $ l.code $ '\' does not exist' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_Code( l.component_code, l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Component with the code \'' $ l.component_code $ '\' does not exist' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_component:active"	VALUE = 0>
	<MvASSIGN NAME = "l.layout_component:dt_start"	VALUE = 0>
	<MvASSIGN NAME = "l.layout_component:dt_end"	VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'R',	l.provide_xml, 'Name',			l.layout_component:name )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Code( 		'R',	l.provide_xml, 'Code',			l.layout_component:code )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Boolean(	'O',	l.provide_xml, 'Active',		l.layout_component:active )			OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text(		'O',	l.provide_xml, 'Parent',		l.parent_code )						OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Date(				l.provide_xml, 'Date_Start',	l.layout_component:dt_start )		OR
					NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Date(				l.provide_xml, 'Date_End',		l.layout_component:dt_end ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Date_Start' ) OR
				    [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Date_End' ) }">
		<MvIF EXPR = "{ l.layout_component:dt_start AND l.layout_component:dt_end AND ( l.layout_component:dt_start GT l.layout_component:dt_end ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Date_End must specify a date/time after Date_Start' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ LayoutComponent_Load_Code( l.layout:id, l.layout_component:code, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Layout Component with the code \'' $ l.layout_component:code $ '\' already exists.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.parent_code }">
		<MvIF EXPR = "{ NOT LayoutComponent_Parent_Load_Code( l.parent_code, l.layout:id, l.loaded_parent ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Could not load Parent with code \'' $ l.parent_code $ '\' in layout \'' $ l.layout:code $ '\'.' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.layout_component:parent" VALUE = "{ l.loaded_parent:id }">
	<MvELSE>
		<MvASSIGN NAME = "l.layout_component:parent" VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ l.layout:id }">
	<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ l.component:id }">
	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ LayoutComponent_DisplayOrder_Last( l.layout_component:layout_id, l.layout_component:parent ) }">

	<MvIF EXPR = "{ NOT LayoutComponent_Insert( l.layout_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml ) }">
	</MvIF>

	<MvIF EXPR = "{ ComponentAttributes_Load_Component( l.component:id, l.component:attributes ) }">
		<MvIF EXPR = "{ [ g.Module_Feature_PRV_AD ].PRV_Tag_Exists( l.provide_xml, 'Attributes' ) }">
			<MvIF EXPR = "{ NOT Module_Provision_LayoutComponent_Attributes( l.provide_xml:tags:Attributes[ 1 ], l.layout_component:id, l.layout_component:cmpnt_id, l.child_pos ) }">
				<MvASSIGN NAME = "l.null" VALUE = "{ LayoutComponent_Delete( l.layout_component:id ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml:children[ l.child_pos ] ) }">
			</MvIF>
		</MvIF>
		
		<MvIF EXPR = "{ NOT LayoutComponents_DisplayOrder_Update_Values( l.layout_id, l.layout_component:disp_order ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogError( l.provide_xml:children[ l.child_pos ] ) }">
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_LayoutComponent_Attributes" PARAMETERS = "provide_xml var, lc_id, cmpnt_id, child_pos var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.child_pos"		VALUE = 1>
	<MvASSIGN NAME = "l.child_count"	VALUE = "{ miva_array_elements( l.provide_xml:children ) }">

	<MvWHILE EXPR = "{ l.child_pos LE l.child_count }">
		<MvASSIGN NAME = "l.name" VALUE = "{ tolower( l.provide_xml:children[ l.child_pos ]:name ) }">

		<MvIF EXPR = "{ l.name NE 'attribute' }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Unknown tag' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.layout_value" VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml:children[ l.child_pos ], 'code', l.layout_value:code ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT ComponentAttribute_Load_Code( l.cmpnt_id, l.layout_value:code, l.component_option ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'A component attribute with the code \'' $ l.layout_value:code $ '\' does not exist' ) }">
		</MvIF>

		<MvIF EXPR = "{ l.component_option:type EQ 'link' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_List( 'R', l.provide_xml:children[ l.child_pos ], 'LinkType', l.layout_value:value:type, 'None,URL,Product,Category,Page', 'N,U,P,C,G' ) }">
				<MvFUNCTIONRETURN>
			</MvIF>

			<MvIF EXPR = "{ l.layout_value:value:type NE 'N' }">
				<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml:children[ l.child_pos ], 'Value', l.layout_value:value:value ) }">
					<MvFUNCTIONRETURN>
				</MvIF>
			</MvIF>

			<MvASSIGN NAME = "l.layout_value:value" VALUE = "{ miva_array_serialize( l.layout_value:value ) }">
		<MvELSEIF EXPR = "{ l.component_option:type EQ 'datetime' }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Date( l.provide_xml:children[ l.child_pos ], 'Value', l.layout_value:value ) }">
				<MvFUNCTIONRETURN>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Tag_Text( 'R', l.provide_xml:children[ l.child_pos ], 'Value', l.layout_value:value ) }">
				<MvFUNCTIONRETURN>
			</MvIF>

			<MvIF EXPR = "{ l.component_option:type EQ 'product' }">
				<MvASSIGN NAME = "l.layout_value:product:id"		VALUE = 0>
				<MvASSIGN NAME = "l.layout_value:product:code"		VALUE = "{ l.layout_value:value }">

				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code( l.layout_value:product:code, l.loadedproduct ) }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Product Code \'' $ l.layout_value:product:code $ '\' was not found.' ) }">
				</MvIF>

				<MvASSIGN NAME = "l.layout_value:product:id"		VALUE = "{ l.loadedproduct:id }">
				<MvASSIGN NAME = "l.layout_value:value"				VALUE = "{ miva_array_serialize( l.layout_value:product ) }">
			<MvELSEIF EXPR = "{ l.component_option:type EQ 'category' }">
				<MvASSIGN NAME = "l.layout_value:category:id"		VALUE = 0>
				<MvASSIGN NAME = "l.layout_value:category:code"		VALUE = "{ l.layout_value:value }">

				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Category_Load_Code( l.layout_value:category:code, l.loadedcategory ) }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Category Code \'' $ l.layout_value:category:code $ '\' was not found.' ) }">
				</MvIF>

				<MvASSIGN NAME = "l.layout_value:category:id"		VALUE = "{ l.loadedcategory:id }">
				<MvASSIGN NAME = "l.layout_value:value"				VALUE = "{ miva_array_serialize( l.layout_value:category ) }">
			<MvELSEIF EXPR = "{ l.component_option:type EQ 'imagetype' }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].ImageType_Load_Code( l.layout_value:value, l.imagetype ) }">
					<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml:children[ l.child_pos ], 'Image Type Code \'' $ l.layout_value:value $ '\' was not found.' ) }">
				</MvIF>

				<MvASSIGN NAME = "l.layout_value:value" VALUE = "{ l.imagetype:id }">
			</MvIF>
		</MvIF>

		<MvASSIGN NAME = "l.layout_value:lc_id"		VALUE = "{ l.lc_id }">
		<MvASSIGN NAME = "l.layout_value:attr_id"	VALUE = "{ l.component_option:id }">

		<MvIF EXPR = "{ NOT LayoutValue_Insert( l.layout_value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.child_pos" VALUE = "{ l.child_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	export
|
</MvCOMMENT>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_File"				VALUE = "{ trim( g.LayoutAndComponentsExport_File ) }">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Email"			VALUE = "{ trim( g.LayoutAndComponentsExport_Email ) }">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_SendEmail"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.LayoutAndComponentsExport_SendEmail ) }">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Option"			VALUE = "{ trim( g.LayoutAndComponentsExport_Option ) }">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_LayoutOption"		VALUE = "{ int( g.LayoutAndComponentsExport_LayoutOption ) }">

	<MvIF EXPR = "{ ISNULL g.LayoutAndComponentsExport_File }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.LayoutAndComponentsExport_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_SendEmail AND len( g.LayoutAndComponentsExport_Email ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_Email', 'Please specify a valid email address' ) }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.LayoutAndComponentsExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Option NE 'components' AND
					g.LayoutAndComponentsExport_Option NE 'layouts' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_Option', 'Please select an export option' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'layouts' AND
					g.LayoutAndComponentsExport_LayoutOption GT 0 }">
		<MvIF EXPR = "{ NOT Layout_Load_ID( g.LayoutAndComponentsExport_LayoutOption, l.layout ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'LayoutAndComponentsExport_LayoutOption', 'Please select a valid layout' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.directory"										VALUE = "{ g.MerchantPath $ 's' $ padl( g.Store:id, 2, '0' ) $ '/export/' }">
	<MvASSIGN NAME = "l.output_file"									VALUE = "{ l.directory $ g.LayoutAndComponentsExport_File }">
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_Time_Start"	VALUE = "{ s.time_t }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Component & Layouts Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Initialize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ LayoutAndComponentsExport_Finalize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'components' }">
		<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_Components( l.output_file, g.LayoutAndComponentsExport_Export_ExportedCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'layouts' }">
		<MvIF EXPR = "{ g.LayoutAndComponentsExport_LayoutOption EQ 0 }">
			<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_Layouts( l.output_file, g.LayoutAndComponentsExport_Export_ExportedCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount, g.LayoutAndComponents_Export_ExportedLayoutsCount, g.LayoutAndComponents_Export_ExportedLayoutsTotalCount ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_Layout( l.output_file, g.LayoutAndComponentsExport_LayoutOption, g.LayoutAndComponentsExport_Export_ExportedCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount, g.LayoutAndComponents_Export_ExportedLayoutsCount, g.LayoutAndComponents_Export_ExportedLayoutsTotalCount ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Calculate_Counts( l.exported_count, l.total_count ) }">

	<MvIF EXPR = "{ l.exported_count GE l.total_count }">
		<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_NeedRefresh" VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "g.LayoutAndComponentsExport_Working_Append" VALUE = 1>

		<MvHIDE FIELDS = "g.Module_Code, g.LayoutAndComponentsExport_Export_Initialized, g.Reset, g.LayoutAndComponentsExport_Working_Append,
						  g.LayoutAndComponentsExport_File, g.LayoutAndComponentsExport_Append, g.LayoutAndComponentsExport_Email, g.LayoutAndComponentsExport_SendEmail,
						  g.LayoutAndComponentsExport_Option, g.LayoutAndComponentsExport_LayoutOption, g.LayoutAndComponentsExport_Export_ExportedCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount,
						  g.LayoutAndComponents_Export_ExportedLayoutsCount, g.LayoutAndComponents_Export_ExportedLayoutsTotalCount, g.LayoutAndComponents_Export_LastLayoutID">
		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.Load_Fields }">
		<MvIF EXPR = "{ g.Reset NE 'No' }">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_File"			VALUE = "">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_Append"		VALUE = "">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_SendEmail"	VALUE = 0>

			<MvASSIGN NAME = "g.LayoutAndComponentsExport_Option"		VALUE = "components">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_LayoutOption"	VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.LayoutAndComponentsExport_File }">		<MvASSIGN NAME = "g.LayoutAndComponentsExport_File"		VALUE = "components_and_layouts.xml">	</MvIF>
		<MvIF EXPR = "{ ISNULL g.LayoutAndComponentsExport_Append }">	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Append"	VALUE = 0>								</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export Components & Layouts to XML File', 'EXPT', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Components & Layouts to XML File', 'EXPT', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

	<script language="JavaScript">
		HaveCustomReset = 1;

		function CustomReset()
		{
			document.EXPT.Reset.value = 'Yes';
		}
	</script>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export Components & Layouts to XML File', 'EXPT', '' ) }">
	<input type="hidden" name="Reset" value="No">
	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	<table width="100%">
		<tr>
			<td nowrap>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Destination', 110 ) }">
				<tr>
					<td nowrap>
						<font face="Arial, Helvetica" size=-1><b>Export Component & Layouts to File:</font></b>
					</td>
					<td width="100%">
						<input type="text" name="LayoutAndComponentsExport_File" size="40" value="{ encodeentities( g.LayoutAndComponentsExport_File ) }">
					</td>
				</tr>

				<tr>
					<td valign="top" nowrap>
						<b>If File Exists:</b>
					</td>
					<td width="100%">
						<MvIF EXPR = "{ g.LayoutAndComponentsExport_Append }">
							<label><input type="radio" name="LayoutAndComponentsExport_Append" value="Yes" checked>Append To File<br></label>
							<label><input type="radio" name="LayoutAndComponentsExport_Append" value="">Replace File</label>
						<MvELSE>
							<label><input type="radio" name="LayoutAndComponentsExport_Append" value="Yes">Append To File</label>

							<br>

							<label><input type="radio" name="LayoutAndComponentsExport_Append" value="" checked>Replace File</label>
						</MvIF>
					</td>
				</tr>

				<tr>
					<td valign="top" nowrap>
						Email File To:
					</td>
					<td width="100%" nowrap>
						<MvIF EXPR = "{ NOT g.LayoutAndComponentsExport_SendEmail }">
							<label><input type="radio" name="LayoutAndComponentsExport_SendEmail" value="0" checked>Do Not Email<br></label>
							<input type="radio" name="LayoutAndComponentsExport_SendEmail" value=1>
						<MvELSE>
							<label><input type="radio" name="LayoutAndComponentsExport_SendEmail" value="0">Do Not Email<br></label>
							<input type="radio" name="LayoutAndComponentsExport_SendEmail" value="1" checked>
						</MvIF>

						<input type="text" size="40" name="LayoutAndComponentsExport_Email" value="{ encodeentities( g.Store:email ) }">
					</td>
				</tr>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
		</tr>
		<tr>
			<td nowrap>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Options', 110 ) }">
				<tr>
					<td nowrap>
						<font face="Arial, Helvetica" size=-1><b>Export:</font></b>
					</td>
					<td width="100%">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'LayoutAndComponentsExport_Option', 'components',	g.LayoutAndComponentsExport_Option, 'Components' ) }"><br>
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'LayoutAndComponentsExport_Option', 'layouts',		g.LayoutAndComponentsExport_Option, '' ) }">

						<select name="LayoutAndComponentsExport_LayoutOption">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 0, g.LayoutAndComponentsExport_LayoutOption, 'All Layouts' ) }">
							<MvFOREACH ITERATOR = "l.layout" ARRAY = "l.layouts" COUNT = "{ Layout_Load_All( l.layouts ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( l.layout:id, g.LayoutAndComponentsExport_LayoutOption, l.layout:name ) }">
							</MvFOREACH>
						</select>
					</td>
				</tr>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
		</tr>
	</table>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Initialize_Export" PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Export_Initialized }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_ExportedCount"			VALUE = 0>
	<MvASSIGN NAME = "g.LayoutAndComponents_Export_ExportedLayoutsCount"		VALUE = 0>
	<MvASSIGN NAME = "g.LayoutAndComponents_Export_LastLayoutID"				VALUE = -1>
	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_ExportedTotalCount"	VALUE = 0>
	<MvASSIGN NAME = "g.LayoutAndComponents_Export_ExportedLayoutsTotalCount"	VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].EnsurePathExists( 'data', l.directory ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ fexists( l.output_file ) }">
		<MvIF EXPR = "{ NOT g.LayoutAndComponentsExport_Append }">
			<MvASSIGN NAME = "l.null" VALUE = "{ fdelete( l.output_file ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'components' }">
		<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_ComponentCount( g.LayoutAndComponentsExport_Export_ExportedTotalCount ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'layouts' }">
		<MvIF EXPR = "{ g.LayoutAndComponentsExport_LayoutOption EQ 0 }">
			<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_AllLayoutsCount( g.LayoutAndComponents_Export_ExportedLayoutsTotalCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_LayoutCount( g.LayoutAndComponentsExport_LayoutOption, g.LayoutAndComponents_Export_ExportedLayoutsTotalCount, g.LayoutAndComponentsExport_Export_ExportedTotalCount ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '<Module code="TGCOMPONENTS" feature="util">' ) }">

	<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_Initialized" VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Finalize_Export" PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Calculate_Counts( l.exported_count, l.total_count ) }">

	<MvIF EXPR = "{ l.exported_count GE l.total_count }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '</Module>' ) }">

		<MvIF EXPR = "{ g.LayoutAndComponentsExport_Option EQ 'components' }">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( l.exported_count $ [ g.Module_Library_Utilities ].Plural( l.exported_count, ' Component', ' Components' ) $ ' exported to \'' $ l.output_file $ '\'' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( g.LayoutAndComponentsExport_Export_ExportedCount $ [ g.Module_Library_Utilities ].Plural( l.exported_count, ' Component', ' Components' ) $ ' & ' $ g.LayoutAndComponents_Export_ExportedLayoutsCount $ [ g.Module_Library_Utilities ].Plural( l.exported_count, ' Layout', ' Layouts' ) $ ' exported to \'' $ l.output_file $ '\'' ) }">
		</MvIF>

		<MvIF EXPR = "{ g.LayoutAndComponentsExport_SendEmail }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Utilities ].Send_Email_Attachment( g.LayoutAndComponentsExport_Email, g.LayoutAndComponentsExport_Email, '', 'Miva Merchant Components & Layouts Export', g.LayoutAndComponentsExport_File, l.directory, 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-UTL-CAL-0053', 'LayoutAndComponentsExport Email Sent' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_Components" PARAMETERS = "output_file, export_count var, total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.search_query"	VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = "{ l.export_count }">
	<MvASSIGN NAME = "l.limit"			VALUE = 5000>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGComponents', 's' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, 'disp_order', 'disp_order', 'disp_order' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0054', g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGComponents', l.search_sql, l.search_fields, l.offset, l.limit) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0055', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGComponents.d.EOF AND NOT g.LayoutAndComponentsExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "l.component_attributes"	VALUE = "">
		<MvASSIGN NAME = "l.export_count"			VALUE = "{ l.export_count + 1 }">

		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Calculate_Counts( l.display_exported_count, l.display_total_count ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( l.display_exported_count, l.display_total_count ) }">

		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	<Component_Add>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Code>' $ TGComponents.d.code $ '</Code>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Name>' $ TGComponents.d.name $ '</Name>' ) }">
		<MvIF EXPR = "{ TGComponents.d.descrip }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Descrip>' $ [ g.Module_Library_Utilities ].CDATA_Encode( TGComponents.d.descrip ) $ '</Descrip>' ) }">
		</MvIF>
		<MvIF EXPR = "{ TGComponents.d.image }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Image>' $ [ g.Module_Library_Utilities ].CDATA_Encode( TGComponents.d.image ) $ '</Image>' ) }">
		</MvIF>
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Allow_Children>' $ int( TGComponents.d.alw_chldrn ) $ '</Allow_Children>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	</Component_Add>' ) }">

		<MvIF EXPR = "{ ComponentAttributes_Load_Component( TGComponents.d.id, l.component_attributes ) }">
			<MvFOREACH ITERATOR = "l.component_attribute" ARRAY = "l.component_attributes">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	<ComponentAttribute_Add component="' $ TGComponents.d.code $ '">' ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Code>'		$ l.component_attribute:code													$ '</Code>' ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Prompt>'	$ [ g.Module_Library_Utilities ].CDATA_Encode( l.component_attribute:prompt )	$ '</Prompt>' ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Type>'		$ l.component_attribute:type													$ '</Type>' ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Required>'	$ l.component_attribute:required												$ '</Required>' ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	</ComponentAttribute_Add>' ) }">
				<MvIF EXPR = "{ ( l.component_attribute:type EQ 'select' OR l.component_attribute:type EQ 'radio' ) AND
								( l.component_attribute:option_count GT 0 ) }">
					<MvFOREACH ITERATOR = "l.component_option" ARRAY = "l.component_attribute:options">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	<ComponentAttributeOption_Add component="' $ TGComponents.d.code $ '" code="' $ l.component_attribute:code $ '">' ) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Prompt>'	$ l.component_option:prompt $ '</Prompt>' ) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	</ComponentAttributeOption_Add>' ) }">
					</MvFOREACH>
				</MvIF>
			</MvFOREACH>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-UTL-CAL-0056', 'Component \'' $ TGComponents.d.code $ '\' exported' ) }">

		<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
			<MvASSIGN NAME = "l.end_time" VALUE = "20">
		<MvELSE>
			<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - g.LayoutAndComponentsExport_Export_Time_Start ) GT l.end_time }">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_NeedRefresh" VALUE = 1>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Export_NeedRefresh AND TGComponents.d.EOF }">
		<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_NeedRefresh"  VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_Layout_LowLevel" PARAMETERS = "output_file, layout var, export_count var, total_count var, layout_export_count var, layout_total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.LayoutAndComponents_Export_LastLayoutID NE l.layout:id }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	<Layout_Add>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Code>' $ l.layout:code $ '</Code>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Name>' $ [ g.Module_Library_Utilities ].CDATA_Encode( l.layout:name ) $ '</Name>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	</Layout_Add>' ) }">

		<MvASSIGN NAME = "g.LayoutAndComponents_Export_LastLayoutID"	VALUE = "{ l.layout:id }">
		<MvASSIGN NAME = "l.layout_export_count"						VALUE = "{ l.layout_export_count + 1 }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-UTL-CAL-0057', 'Layout \'' $ l.layout:code $ '\' exported' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.search_query"	VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = "{ l.export_count }">
	<MvASSIGN NAME = "l.limit"			VALUE = 5000>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGLayoutComponents', 's' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 's.layout_id = ?', 'g.LayoutAndComponents_Export_LastLayoutID' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, 'disp_order', 'disp_order', 'disp_order' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0058', g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGLayoutComponentsExport', l.search_sql, l.search_fields, l.offset, l.limit) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0059', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutComponentsExport.d.EOF AND NOT g.LayoutAndComponentsExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "l.export_count" VALUE = "{ l.export_count + 1 }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Calculate_Counts( l.display_exported_count, l.display_total_count ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( l.display_exported_count, l.display_total_count ) }">

		<MvIF EXPR = "{ NOT Component_Load_ID( TGLayoutComponentsExport.d.cmpnt_id, l.component ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponentsExport">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	<LayoutComponent_Add layout="' $ l.layout:code $ '" component="' $ l.component:code $ '">' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Code>' 	$ TGLayoutComponentsExport.d.code													$ '</Code>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Name>' 	$ [ g.Module_Library_Utilities ].CDATA_Encode( TGLayoutComponentsExport.d.name )	$ '</Name>' ) }">
		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Active>'	$ int( TGLayoutComponentsExport.d.active )											$ '</Active>' ) }">

		<MvIF EXPR = "{ TGLayoutComponentsExport.d.parent GT 0 }">
			<MvIF EXPR = "{ NOT LayoutComponent_Load_ID( TGLayoutComponentsExport.d.parent, l.parent_component ) }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponentsExport">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Parent>' $ l.parent_component:code $ '</Parent>' ) }">
		</MvIF>

		<MvIF EXPR = "{ TGLayoutComponentsExport.d.dt_start }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Date_Start>' ) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Day>'		$ padl( time_t_dayofmonth( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),	2, '0' ) $ '</Day>' 	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Month>'	$ padl( time_t_month( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Month>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Year>'		$ padl( time_t_year( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),			4, '0' ) $ '</Year>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Hour>'		$ padl( time_t_hour( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Hour>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Minute>'	$ padl( time_t_min( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Minute>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Second>'	$ padl( time_t_sec( TGLayoutComponentsExport.d.dt_start, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Second>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		</Date_Start>' ) }">
		</MvIF>

		<MvIF EXPR = "{ TGLayoutComponentsExport.d.dt_end }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Date_End>' ) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Day>'		$ padl( time_t_dayofmonth( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),	2, '0' ) $ '</Day>' 	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Month>'	$ padl( time_t_month( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),		2, '0' ) $ '</Month>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Year>'		$ padl( time_t_year( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),		4, '0' ) $ '</Year>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Hour>'		$ padl( time_t_hour( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),		2, '0' ) $ '</Hour>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Minute>'	$ padl( time_t_min( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Minute>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Second>'	$ padl( time_t_sec( TGLayoutComponentsExport.d.dt_end, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Second>'	) }">
			<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		</Date_End>' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.attributes"		VALUE = "">
		<MvASSIGN NAME = "l.layout_values"	VALUE = "">

		<MvIF EXPR = "{	ComponentAttributes_Load_All( TGLayoutComponentsExport.d.cmpnt_id, l.attributes ) }">
			<MvIF EXPR = "{ LayoutComponent_Load_Values( TGLayoutComponentsExport.d.id, l.attributes ) AND miva_array_filter( l.attributes, 1, l.attribute, 'NOT ISNULL l.attribute:value', l.attributes ) }">
				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		<Attributes>' ) }">
				<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes">
					<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			<Attribute code="' $ l.attribute:code $ '">' ) }">

					<MvIF EXPR = "{ l.attribute:type EQ 'link' }">
						<MvIF EXPR = "{ l.attribute:link:type EQ 'U' }">		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<LinkType>URL</LinkType>' ) }">
						<MvELSEIF EXPR = "{ l.attribute:link:type EQ 'P' }">	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<LinkType>Product</LinkType>' ) }">
						<MvELSEIF EXPR = "{ l.attribute:link:type EQ 'C' }">	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<LinkType>Category</LinkType>' ) }">
						<MvELSEIF EXPR = "{ l.attribute:link:type EQ 'G' }">	<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<LinkType>Page</LinkType>' ) }">
						<MvELSE>												<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<LinkType>None</LinkType>' ) }">
						</MvIF>

						<MvIF EXPR = "{ l.attribute:link:type NE 'N' AND NOT ISNULL l.attribute:link:value }">
							<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ [ g.Module_Library_Utilities ].CDATA_Encode( l.attribute:link:value ) $ '</Value>' ) }">
						</MvIF>
					<MvELSEIF EXPR = "{ l.attribute:type EQ 'product' }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ l.attribute:product:code $ '</Value>' ) }">
					<MvELSEIF EXPR = "{ l.attribute:type EQ 'category' }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ l.attribute:category:code $ '</Value>' ) }">
					<MvELSEIF EXPR = "{ l.attribute:type EQ 'imagetype' }">
						<MvIF EXPR = "{ [ g.Module_Library_DB ].ImageType_Load_ID( l.attribute:value, l.imagetype ) }">
							<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ l.imagetype:code $ '</Value>' ) }">
						</MvIF>
					<MvELSEIF EXPR = "{ l.attribute:type EQ 'multitext' }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ [ g.Module_Library_Utilities ].CDATA_Encode( miva_joinstring( l.attribute:value, '|', 'trim' ) ) $ '</Value>' ) }">
					<MvELSEIF EXPR = "{ l.attribute:type EQ 'datetime' }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' ) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Day>'		$ padl( time_t_dayofmonth( l.attribute:value, g.Merchant_Local_Timezone ),	2, '0' ) $ '</Day>' 	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Month>'	$ padl( time_t_month( l.attribute:value, g.Merchant_Local_Timezone ),		2, '0' ) $ '</Month>'	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Year>'		$ padl( time_t_year( l.attribute:value, g.Merchant_Local_Timezone ),		4, '0' ) $ '</Year>'	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Hour>'		$ padl( time_t_hour( l.attribute:value, g.Merchant_Local_Timezone ),		2, '0' ) $ '</Hour>'	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Minute>'	$ padl( time_t_min( l.attribute:value, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Minute>'	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '					<Second>'	$ padl( time_t_sec( l.attribute:value, g.Merchant_Local_Timezone ),			2, '0' ) $ '</Second>'	) }">
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				</Value>' ) }">
					<MvELSE>
						<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '				<Value>' $ [ g.Module_Library_Utilities ].CDATA_Encode( l.attribute:value ) $ '</Value>' ) }">
					</MvIF>
					<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '			</Attribute>' ) }">
				</MvFOREACH>

				<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '		</Attributes>' ) }">
			</MvIF>
		</MvIF>

		<MvEVAL EXPR = "{ LayoutAndComponentsExport_Export_Line( l.output_file, '	</LayoutComponent_Add>' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-UTL-CAL-0060', 'Layout Component \'' $ TGLayoutComponentsExport.d.code $ '\' exported' ) }">

		<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
			<MvASSIGN NAME = "l.end_time" VALUE = "20">
		<MvELSE>
			<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - g.LayoutAndComponentsExport_Export_Time_Start ) GT l.end_time }">
			<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_NeedRefresh" VALUE = 1>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponentsExport" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ g.LayoutAndComponentsExport_Export_NeedRefresh AND TGLayoutComponentsExport.d.EOF }">
		<MvASSIGN NAME = "g.LayoutAndComponentsExport_Export_NeedRefresh"  VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponentsExport">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_Layout" PARAMETERS = "output_file, layout_id, export_count var, total_count var, layout_export_count var, layout_total_count var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_ID( l.layout_id, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_Layout_LowLevel( l.output_file, l.layout, l.export_count, l.total_count, l.layout_export_count, l.layout_total_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_Layouts" PARAMETERS = "output_file, export_count var, total_count var, layout_export_count var, layout_total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.export_count GE l.total_count }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.search_query"	VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = "{ l.layout_export_count }">
	<MvASSIGN NAME = "l.limit"			VALUE = 5000>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 's.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGLayouts', 's' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.component_layouts_total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0061', g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGLayouts', l.search_sql, l.search_fields, l.offset, l.limit) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0062', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayouts.d.EOF AND NOT g.LayoutAndComponentsExport_Export_NeedRefresh }">
		<MvEVAL EXPR = "{ Layout_Read( l.layout ) }">

		<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_Layout_LowLevel( l.output_file, l.layout, l.export_count, l.total_count, l.layout_export_count, l.layout_total_count ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayouts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_ComponentCount" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'TGComponents' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0063', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ TGComponents.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_AllLayoutsCount" PARAMETERS = "layouts_total var, total_count var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutAndComponentsExport_Export_LayoutsCount( l.layouts_total ) OR
					NOT LayoutAndComponentsExport_Export_AllLayoutComponentsCount( l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_LayoutsCount" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'TGLayouts' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0064', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ TGLayouts.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_AllLayoutComponentsCount" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0065', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ TGLayoutComponents.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_LayoutCount" PARAMETERS = "layout_id, layouts_total var, total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layouts_total" VALUE = 1>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ?' }"
				FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0066', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ TGLayoutComponents.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Export_Line" PARAMETERS = "output, line" STANDARDOUTPUTLEVEL = "">
	<MvEXPORT FILE = "{ l.output }" DELIMITER = "" FIELDS = "l.line">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutAndComponentsExport_Calculate_Counts" PARAMETERS = "exported_count var, total_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.exported_count"	VALUE = 0>
	<MvASSIGN NAME = "l.total_count"	VALUE = 0>

	<MvASSIGN NAME = "l.exported_count"	VALUE = "{ g.LayoutAndComponentsExport_Export_ExportedCount + g.LayoutAndComponents_Export_ExportedLayoutsCount }">
	<MvASSIGN NAME = "l.total_count" 	VALUE = "{ g.LayoutAndComponentsExport_Export_ExportedTotalCount + g.LayoutAndComponents_Export_ExportedLayoutsTotalCount }">
</MvFUNCTION>

<MvCOMMENT>
|
|	scheduledtask
|
</MvCOMMENT>

<MvFUNCTION NAME = "ScheduledTaskModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings" VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Operations" PARAMETERS = "module var, operations var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.operations" INDEX = 1 MEMBER = "code"		VALUE = "check_layout_cache">
	<MvASSIGN NAME = "l.operations" INDEX = 1 MEMBER = "descrip"	VALUE = "Layout Cache & Scheduled Component Check">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Fields" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Validate" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Update" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Delete" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Execute" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.task:operation NE 'check_layout_cache' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0067', 'Unsupported operation' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Check_All() }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'W', g.Error_Code $ ': ' $ g.Error_Message ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Provision_Settings" PARAMETERS = "module var, task var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponents
|
</MvCOMMENT>

<MvFUNCTION NAME = "Component_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component:id"				VALUE = "{ TGComponents.d.id }">
	<MvASSIGN NAME = "l.component:code"				VALUE = "{ TGComponents.d.code }">
	<MvASSIGN NAME = "l.component:name"				VALUE = "{ TGComponents.d.name }">
	<MvASSIGN NAME = "l.component:descrip"			VALUE = "{ TGComponents.d.descrip }">
	<MvASSIGN NAME = "l.component:image"			VALUE = "{ TGComponents.d.image }">
	<MvASSIGN NAME = "l.component:alw_chldrn"		VALUE = "{ TGComponents.d.alw_chldrn }">
	<MvASSIGN NAME = "l.component:disp_order"		VALUE = "{ TGComponents.d.disp_order }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.component:allow_children"	VALUE = "{ TGComponents.d.alw_chldrn }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Insert" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponents' ) }">
	<MvASSIGN NAME = "l.component:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponents
						  ( id, code, name, descrip, image, alw_chldrn, disp_order )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.component:id, l.component:code, l.component:name, l.component:descrip, l.component:image, l.component:alw_chldrn, l.component:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0068', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Load_ID" PARAMETERS = "id, component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0069', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0070' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Component_Read( l.component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Load_Code" PARAMETERS = "code, component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0071', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0072' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Component_Read( l.component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Update" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponents
						  SET
							code = ?,
							name = ?,
							descrip = ?,
							image = ?,
							alw_chldrn = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.component:code, l.component:name, l.component:descrip, l.component:image, l.component:alw_chldrn, l.component:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0073', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Component_Delete_LowLevel( l.id )				OR
					NOT ComponentAttributes_Delete_Component( l.id )	OR
					NOT LayoutComponents_Delete_Component( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0074', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Components_Update_Offsets" PARAMETERS = "components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"						VALUE = 0>

	<MvASSIGN NAME = "l.state_pos"							VALUE = 1>
	<MvASSIGN NAME = "l.state_count"						VALUE = "{ miva_array_elements( l.components ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.components, l.state_count ) }">

	<MvWHILE EXPR = "{ l.state_pos LE l.state_count }">
		<MvASSIGN NAME = "g.Components_Order_ID"		VALUE = "{ l.components[ l.state_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current state at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponents',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponents WHERE id <> ? ORDER BY disp_order',
																				   'g.Components_Order_ID',
																				   l.components[ l.state_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0075', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponents.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.components[ l.state_pos ] }">

			<MvASSIGN NAME = "l.state_pos"	VALUE = "{ l.state_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvIF EXPR = "{ NOT Components_Update_Offsets_PastEnd( l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponents.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

			<MvCOMMENT>
			|
			| Make a hole by shifting components after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' 
									$ g.Store_Table_Prefix $ 'TGComponents 
								  SET 
									disp_order = disp_order + 1 
								  WHERE 
									disp_order>= ?' }" 
					 FIELDS	= "l.disp_order">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0076', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the state in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME	= "Merchant"
					 QUERY	= "{ 'UPDATE ' 
									$ g.Store_Table_Prefix $ 'TGComponents 
								  SET 
									disp_order = ? 
								  WHERE 
									id = ?' }" 
					 FIELDS	= "l.disp_order, g.Components_Order_ID">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0077', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.state_pos"	VALUE = "{ l.state_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT Components_Update_Offsets_PastEnd( l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Components_Update_Offsets_PastEnd" PARAMETERS = "pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Components_Order_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"				VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentDisplayOrder' ) }">

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'UPDATE ' 
								$ g.Store_Table_Prefix $ 'TGComponents 
							  SET 
								disp_order = ? 
							  WHERE 
								id = ?' }" 
				 FIELDS	= "l.disp_order, g.Components_Order_ID">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0078', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Layout_Reference" PARAMETERS = "component_id, layouts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT
								DISTINCT( lc.layout_id ) AS layout_id,
								l.code AS layout_code
							  FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents lc
							  LEFT JOIN ' $ g.Store_Table_Prefix $ 'TGLayouts l
							  ON lc.layout_id = l.id
							  WHERE
							  	component_id = ?' }"
				FIELDS	= "l.component_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0079', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layouts_count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvASSIGN NAME = "l.temp:id"		VALUE = "{ TGLayoutComponents.d.layout_id }">
		<MvASSIGN NAME = "l.temp:code"		VALUE = "{ TGLayoutComponents.d.layout_code }">
		<MvASSIGN NAME = "l.layouts_count"	VALUE = "{ miva_array_insert_var( l.layouts, l.temp, -1 ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0080', l.layouts_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentList_Load_All" PARAMETERS = "components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponents ORDER BY disp_order' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0081', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0082' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGComponents.d.EOF }">
		<MvEVAL EXPR = "{ Component_Read( l.components[ ++l.count ] ) }">

		<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.components[ l.count ]:id, l.components[ l.count ]:attributes ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0083', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponentAttributes
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentAttribute_Read" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_attribute:id"				VALUE = "{ TGComponentAttributes.d.id }">
	<MvASSIGN NAME = "l.component_attribute:cmpnt_id"		VALUE = "{ TGComponentAttributes.d.cmpnt_id }">
	<MvASSIGN NAME = "l.component_attribute:code"			VALUE = "{ TGComponentAttributes.d.code }">
	<MvASSIGN NAME = "l.component_attribute:prompt"			VALUE = "{ TGComponentAttributes.d.prompt }">
	<MvASSIGN NAME = "l.component_attribute:type"			VALUE = "{ TGComponentAttributes.d.type }">
	<MvASSIGN NAME = "l.component_attribute:required"		VALUE = "{ TGComponentAttributes.d.required }">
	<MvASSIGN NAME = "l.component_attribute:disp_order"		VALUE = "{ TGComponentAttributes.d.disp_order }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.component_attribute:component_id"	VALUE = "{ TGComponentAttributes.d.cmpnt_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Insert" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_attribute:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributes' ) }">
	<MvASSIGN NAME = "l.component_attribute:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
						  ( id, cmpnt_id, code, prompt, type, required, disp_order )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.component_attribute:id, l.component_attribute:cmpnt_id, l.component_attribute:code, l.component_attribute:prompt, l.component_attribute:type, l.component_attribute:required, l.component_attribute:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0084', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Load_ID" PARAMETERS = "id, component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0085', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0086' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME ="ComponentAttribute_Load_Code" PARAMETERS = "cmpnt_id, code, component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.cmpnt_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0087', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0088' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attribute ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update" PARAMETERS = "component_attribute var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponentAttributes
						  SET
							code = ?,
							prompt = ?,
							type = ?,
							required = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.component_attribute:code, l.component_attribute:prompt, l.component_attribute:type, l.component_attribute:required, l.component_attribute:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0089', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Delete" PARAMETERS = "component_attribute_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValues_Delete_Attribute( l.component_attribute_id )		OR
					NOT ComponentOptions_Delete_Attribute( l.component_attribute_id )	OR
					NOT ComponentAttribute_Delete_LowLevel( l.component_attribute_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Delete_LowLevel" PARAMETERS = "component_attribute_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id = ?' }"
			 FIELDS	= "l.component_attribute_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0090', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttributes_Delete_Component" PARAMETERS = "cmpnt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ?' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0091', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Delete( TGComponentAttributes.d.id  ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update_Offsets" PARAMETERS = "cmpnt_id, fields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"			VALUE = 0>

	<MvASSIGN NAME = "l.field_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.field_count"			VALUE = "{ miva_array_elements( l.fields ) }">
	<MvASSIGN NAME = "g.Field_cmpnt_id"		VALUE = "{ l.cmpnt_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.fields, l.field_count ) }">

	<MvWHILE EXPR = "{ l.field_pos LE l.field_count }">
		<MvASSIGN NAME = "g.ComponentAttribute_ID" 	VALUE = "{ l.fields[ l.field_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current field at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponentAttributes',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE id <> ? AND cmpnt_id = ? ORDER BY disp_order',
																				   'g.ComponentAttribute_ID, g.Field_cmpnt_id',
																				   l.fields[ l.field_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0092', g.Error_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.fields[ l.field_pos ] }">

			<MvASSIGN NAME = "l.field_pos"	VALUE = "{ l.field_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets_PastEnd( l.cmpnt_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponentAttributes.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

			<MvCOMMENT>
			|
			| Make a hole by shifting fields after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentAttributes 
													SET 
														disp_order = disp_order + 1 
													WHERE 
														disp_order>= ? AND cmpnt_id = ?' }" 
													FIELDS = "l.disp_order, l.cmpnt_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0093', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the field in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentAttributes 
													SET 
														disp_order = ? 
													WHERE 
														id = ? AND cmpnt_id = ?' }" 
													FIELDS = "l.disp_order, g.ComponentAttribute_ID, l.cmpnt_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0094', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.field_pos"	VALUE = "{ l.field_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets_PastEnd( l.cmpnt_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Update_Offsets_PastEnd" PARAMETERS = "cmpnt_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos" VALUE = 1>

	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.ComponentAttribute_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"				VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentAttributeDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'TGComponentAttributes 
												SET 
													disp_order = ? 
												WHERE 
													id = ? AND cmpnt_id = ?' }" 
												FIELDS = "l.disp_order, g.ComponentAttribute_ID, l.cmpnt_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0095', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttribute_Types" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "radio,select,checkbox,text,memo,image,product,category,link,imagetype,datetime,multitext">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttributes_Load_Component" PARAMETERS = "cmpnt_id, component_attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0096', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0097' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
		<MvASSIGN NAME = "l.count"	VALUE = "{ l.count + 1 }">
		<MvASSIGN NAME = "l.null"	VALUE = "{ ComponentAttribute_Read( l.component_attributes[ l.count ] ) }">

		<MvIF EXPR = "{ ( l.component_attributes[ l.count ]:type EQ 'select' ) OR
						( l.component_attributes[ l.count ]:type EQ 'radio' ) }">
			<MvASSIGN NAME = "l.component_attributes" INDEX = "{ l.count }" MEMBER = "option_count" VALUE = "{ ComponentOptions_Load_Attribute( l.component_attributes[ l.count ]:id, l.component_attributes[ l.count ]:options ) }">
		<MvELSEIF EXPR = "{ l.component_attributes[ l.count ]:type EQ 'imagetype' }">
			<MvASSIGN NAME = "l.component_attributes" INDEX = "{ l.count }" MEMBER = "option_count"	VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes )  }">
			<MvASSIGN NAME = "l.component_attributes" INDEX = "{ l.count }" MEMBER = "options"		VALUE = "{ l.imagetypes }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0098', l.count ) }">
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGComponentOptions
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentOption_Read" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_option:id"			VALUE = "{ TGComponentOptions.d.id }">
	<MvASSIGN NAME = "l.component_option:attr_id"		VALUE = "{ TGComponentOptions.d.attr_id }">
	<MvASSIGN NAME = "l.component_option:prompt"		VALUE = "{ TGComponentOptions.d.prompt }">
	<MvASSIGN NAME = "l.component_option:disp_order"	VALUE = "{ TGComponentOptions.d.disp_order }">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Insert" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.component_option:id"			VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptions' ) }">
	<MvASSIGN NAME = "l.component_option:disp_order"	VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGComponentOptions
						  ( id, attr_id, prompt, disp_order )
						  VALUES
						  ( ?, ?, ?, ? )' }"
			 FIELDS	= "l.component_option:id, l.component_option:attr_id, l.component_option:prompt, l.component_option:disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0099', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Load" PARAMETERS = "id, component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0100', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0101' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ ComponentOption_Read( l.component_option ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Update" PARAMETERS = "component_option var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGComponentOptions SET prompt = ? WHERE id = ?' }"
			 FIELDS	= "l.component_option:prompt, l.component_option:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0102', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOption_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0103', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Delete_Attribute" PARAMETERS = "attr_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE attr_id = ?' }"
			 FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0104', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Load_Attribute" PARAMETERS = "attr_id, component_options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentOptions"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE attr_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0105', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0106' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGComponentOptions.d.EOF }">
		<MvEVAL EXPR = "{ ComponentOption_Read( l.component_options[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "TGComponentOptions" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Update_Offsets" PARAMETERS = "attr_id, options var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.pastend_count"				VALUE = 0>

	<MvASSIGN NAME = "l.option_pos"				VALUE = 1>
	<MvASSIGN NAME = "l.option_count"			VALUE = "{ miva_array_elements( l.options ) }">
	<MvASSIGN NAME = "g.Option_attr_id" 		VALUE = "{ l.attr_id }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray( l.options, l.option_count ) }">

	<MvWHILE EXPR = "{ l.option_pos LE l.option_count }">
		<MvASSIGN NAME = "g.Option_Order_Attribute_ID" VALUE = "{ l.options[ l.option_pos ]:id }">

		<MvCOMMENT>
		|
		| Determine disp_order of current option at this offset.
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant',
																				   'TGComponentOptions',
																				   'SELECT id, disp_order FROM ' $ g.Store_Table_Prefix $ 'TGComponentOptions WHERE id <> ? AND attr_id = ? ORDER BY disp_order',
																				   'g.Option_Order_Attribute_ID, g.Option_attr_id',
																				   l.options[ l.option_pos ]:offset - 1,
																				   1 ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGCD-OPTION-0009', 'Could not load display order for attribute option.' ) }">
		</MvIF>

		<MvIF EXPR = "{ TGComponentOptions.d.EOF }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvCOMMENT>
			|
			| Offset was higher than the last record.  This case requires specialized processing, so save this record for later.
			|
			</MvCOMMENT>

			<MvASSIGN NAME = "l.pastend_count"							VALUE = "{ l.pastend_count + 1 }">
			<MvASSIGN NAME = "l.pastend" INDEX = "{ l.pastend_count }"	VALUE = "{ l.options[ l.option_pos ] }">

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		<MvELSEIF EXPR = "{ l.pastend_count }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets_PastEnd( l.data_id, l.pastend, l.pastend_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.pastend_count" VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.disp_order" VALUE = "{ TGComponentOptions.d.disp_order }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentOptions">

			<MvCOMMENT>
			|
			| Make a hole by shifting options after this disp_order down
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentOptions 
													SET 
														disp_order = disp_order + 1 
													WHERE 
														disp_order>= ? AND attr_id = ?' }" 
													FIELDS = "l.disp_order, l.attr_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0107', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

			<MvCOMMENT>
			|
			| Put the option in the newly created hole
			|
			</MvCOMMENT>

			<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
														$ g.Store_Table_Prefix $ 'TGComponentOptions 
													SET 
														disp_order = ? 
													WHERE 
														id = ? AND attr_id = ?' }" 
													FIELDS = "l.disp_order, g.Option_Order_Attribute_ID, l.attr_id">
			<MvIF EXPR = "{ g.MvQUERY_Error }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0108', g.MvQUERY_Error ) }">
			</MvIF>

			<MvASSIGN NAME = "l.option_pos"	VALUE = "{ l.option_pos + 1 }">
		</MvIF>
	</MvWHILE>

	<MvCOMMENT>
	|
	| If all the downward records were past the end, they must be processed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.pastend_count }">
		<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets_PastEnd( l.data_id, l.pastend, l.pastend_count ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentOptions_Update_Offsets_PastEnd" PARAMETERS = "attr_id, pastend var, pastend_count" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| Process records moving past the end.  These records must be sorted in ascending order.
	|
	</MvCOMMENT>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].SortOffsetArray_PastEnd( l.pastend, l.pastend_count ) }">

	<MvASSIGN NAME = "l.pastend_pos"		VALUE = 1>
	<MvWHILE EXPR = "{ l.pastend_pos LE l.pastend_count }">
		<MvASSIGN NAME = "g.Option_Order_Attribute_ID" 	VALUE = "{ l.pastend[ l.pastend_pos ]:id }">
		<MvASSIGN NAME = "l.disp_order"					VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGComponentOptionDisplayOrder' ) }">

		<MvQUERY NAME = "Merchant" QUERY = "{ 'UPDATE ' 
													$ g.Store_Table_Prefix $ 'TGComponentOptions 
												SET 
													disp_order = ? 
												WHERE 
													id = ? AND attr_id = ?' }" 
												FIELDS = "l.disp_order, g.Option_Order_Attribute_ID, l.attr_id">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0109', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.pastend_pos"	VALUE = "{ l.pastend_pos + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayouts
|
</MvCOMMENT>

<MvFUNCTION NAME = "Layout_Read" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:id"		VALUE = "{ TGLayouts.d.id }">
	<MvASSIGN NAME = "l.layout:code"	VALUE = "{ TGLayouts.d.code }">
	<MvASSIGN NAME = "l.layout:name"	VALUE = "{ TGLayouts.d.name }">
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Insert" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGLayouts' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayouts
						  ( id, code, name )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout:id, l.layout:code, l.layout:name">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0110', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_ID" PARAMETERS = "id, layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0111', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayouts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0112' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Layout_Read( l.layout ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_Code" PARAMETERS = "code, layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0113', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayouts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0114' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Layout_Read( l.layout ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Update" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Update_LowLevel( l.layout ) OR
					NOT LayoutCache_Recache( l.layout:id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Load_All" PARAMETERS = "layouts var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayouts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayouts' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0115', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayouts.d.EOF }">
		<MvEVAL EXPR = "{ Layout_Read( l.layouts[ ++l.count ] ) }">
		<MvSKIP NAME = "Merchant" VIEW = "TGLayouts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0116', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Update_LowLevel" PARAMETERS = "layout var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayouts
						  SET
							code = ?,
							name = ?
						  WHERE
							id = ?' }"
			 FIELDS	= "l.layout:code, l.layout:name, l.layout:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0117', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Delete" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Delete_LowLevel( l.layout_id )			OR
					NOT LayoutComponents_Delete_Layout( l.layout_id )	OR
					NOT LayoutCache_Delete( l.layout_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Delete_LowLevel" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayouts WHERE id = ?' }"
			 FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0118', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Duplicate_LowLevel" PARAMETERS = "layout_id, layout_components var, disp_order var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recursive Function that will duplicate the layout components.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.disp_order }"> <MvASSIGN NAME = "l.disp_order" VALUE = 0> </MvIF>

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components:children">
		<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ l.layout_id }">
		<MvASSIGN NAME = "l.layout_component:id"			VALUE = 0>
		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ int( l.layout_components:id ) }">
		<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ int( l.layout_component:component:id ) }">
		<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ ++l.disp_order }">

		<MvIF EXPR = "{ NOT LayoutComponent_Insert( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_Attributes( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Layout_Duplicate_LowLevel( l.layout_id, l.layout_component, l.disp_order ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Layout_Duplicate" PARAMETERS = "layout_id, duplicate_layout_id" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.disp_order" VALUE = 0>

	<MvIF EXPR = "{ NOT LayoutComponents_Load_Parent( l.duplicate_layout_id, 0, l.layout:children ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Duplicate_LowLevel( l.layout_id, l.layout, l.disp_order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutComponents
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutComponent_Read" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_component:id"			VALUE = "{ TGLayoutComponents.d.id }">
	<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ TGLayoutComponents.d.layout_id }">
	<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ TGLayoutComponents.d.cmpnt_id }">
	<MvASSIGN NAME = "l.layout_component:name"			VALUE = "{ TGLayoutComponents.d.name }">
	<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ TGLayoutComponents.d.parent }">
	<MvASSIGN NAME = "l.layout_component:active"		VALUE = "{ TGLayoutComponents.d.active }">
	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ TGLayoutComponents.d.disp_order }">
	<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = "{ TGLayoutComponents.d.dt_start }">
	<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = "{ TGLayoutComponents.d.dt_end }">
	<MvASSIGN NAME = "l.layout_component:code"			VALUE = "{ TGLayoutComponents.d.code }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.layout_component:component_id"	VALUE = "{ TGLayoutComponents.d.cmpnt_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Insert" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_component:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGLayoutComponents' ) }">

	<MvIF EXPR = "{ ISNULL l.layout_component:parent }">		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:active }">		<MvASSIGN NAME = "l.layout_component:active"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_start }">		<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_end }">		<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:disp_order }">	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
						  ( id, layout_id, cmpnt_id, name, parent, active, disp_order, dt_start, dt_end, code )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.layout_component:id, l.layout_component:layout_id, l.layout_component:cmpnt_id,
			 		   l.layout_component:name, l.layout_component:parent, l.layout_component:active,
			 		   l.layout_component:disp_order, l.layout_component:dt_start, l.layout_component:dt_end,
			 		   l.layout_component:code">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0119', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_ID" PARAMETERS = "id, layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0120', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0121' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_Code" PARAMETERS = "layout_id, code, layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.layout_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0122', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0123' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Update" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.layout_component:parent }">		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_start }">		<MvASSIGN NAME = "l.layout_component:dt_start"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:dt_end }">		<MvASSIGN NAME = "l.layout_component:dt_end"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.layout_component:disp_order }">	<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = 0> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
					      SET
							name = ?,
							parent = ?,
							active = ?,
							disp_order = ?,
							dt_start = ?,
							dt_end = ?,
							code = ?
					      WHERE
						    id = ?' }"
			 FIELDS	= "l.layout_component:name, l.layout_component:parent, l.layout_component:active,
			 		   l.layout_component:disp_order, l.layout_component:dt_start, l.layout_component:dt_end,
			 		   l.layout_component:code, l.layout_component:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0124', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutComponent_Delete_Children( l.id ) OR
					NOT LayoutComponent_Delete_LowLevel( l.id ) OR
					NOT LayoutValues_Delete_LayoutComponent( l.id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Delete_Children" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE parent = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0125', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_components"			VALUE = "">
	<MvASSIGN NAME = "l.layout_components_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_components[ ++l.layout_components_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components" COUNT = "{ l.layout_components_count }">
		<MvASSIGN NAME = "l.null" VALUE = "{ LayoutComponent_Delete( l.layout_component:id ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0126', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Delete_Component" PARAMETERS = "cmpnt_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE cmpnt_id = ?' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0127', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_components"			VALUE = "">
	<MvASSIGN NAME = "l.layout_components_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_components[ ++l.layout_components_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components" COUNT = "{ l.layout_components_count }">
		<MvASSIGN NAME = "l.null" VALUE = "{ LayoutComponent_Delete( l.layout_component:id ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Delete_Layout" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ?' }"
				FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0128', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_components"			VALUE = "">
	<MvASSIGN NAME = "l.layout_components_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_components[ ++l.layout_components_count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components" COUNT = "{ l.layout_components_count }">
		<MvASSIGN NAME = "l.null" VALUE = "{ LayoutComponent_Delete( l.layout_component:id ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MVFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Parent_Load_Code" PARAMETERS = "code, layout_id, layout_component var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents
							  WHERE layout_id = ? AND ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.layout_id, l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0129', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0130' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutComponent_Read( l.layout_component ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_DisplayOrder_Last" PARAMETERS = "layout_id, parent" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.return" VALUE = 0>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT disp_order FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND parent = ? ORDER BY disp_order DESC LIMIT 1' }"
				FIELDS	= "l.layout_id, l.parent">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0131', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutComponents.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.return" VALUE = "{ TGLayoutComponents.d.disp_order + 1 }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">

	<MvFUNCTIONRETURN VALUE = "{ l.return }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_DisplayOrder_Update_Values" PARAMETERS = "layout_id, disp_order" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' 
							$ g.Store_Table_Prefix $ 'TGLayoutComponents 
						  SET 
							disp_order = disp_order + 1 
						  WHERE 
						  	layout_id = ? AND
							disp_order >= ?' }" 
			 FIELDS	= "l.layout_id, l.disp_order">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0132', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutValues
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutValue_Read" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_value:lc_id"					VALUE = "{ TGLayoutValues.d.lc_id  }">
	<MvASSIGN NAME = "l.layout_value:attr_id"				VALUE = "{ TGLayoutValues.d.attr_id }">
	<MvASSIGN NAME = "l.layout_value:value"					VALUE = "{ TGLayoutValues.d.value }">

	<MvCOMMENT>
	|
	|	For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.layout_value:layouts_components_id"	VALUE = "{ TGLayoutValues.d.lc_id  }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Insert" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Insert_LowLevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutValue_Reference_Insert( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Insert_LowLevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutValues
						  ( lc_id, attr_id, value )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout_value:lc_id, l.layout_value:attr_id, l.layout_value:value">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0133', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Load" PARAMETERS = "lc_id, attr_id, layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutValues"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ? AND attr_id = ?' }"
				FIELDS	= "l.lc_id, l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0134', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutValues.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0135' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutValue_Read( l.layout_value ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Update" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Reference_Update( l.layout_value ) OR
					NOT LayoutValue_Update_Lowlevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Update_Lowlevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGLayoutValues
					      SET
							value = ?
					      WHERE
						    lc_id = ? AND
						    attr_id = ?' }"
			 FIELDS	= "l.layout_value:value, l.layout_value:lc_id, l.layout_value:attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0136', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Delete" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT LayoutValue_Reference_Delete( l.layout_value ) OR
					NOT LayoutValue_Delete_LowLevel( l.layout_value ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Delete_LowLevel" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ? AND attr_id = ?' }"
			 FIELDS	= "l.layout_value:lc_id, l.layout_value:attr_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0137', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValues_Delete_Attribute" PARAMETERS = "attr_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutValues"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE attr_id = ?' }"
				FIELDS	= "l.attr_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0138', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutValues.d.EOF }">
		<MvEVAL EXPR = "{ LayoutValue_Read( l.layout_value ) }">

		<MvIF EXPR = "{ NOT LayoutValue_Delete( l.layout_value  ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutValues" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutValues">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValues_Delete_LayoutComponent" PARAMETERS = "lc_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutValues WHERE lc_id = ?' }"
			 FIELDS	= "l.lc_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0139', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Insert" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Increment_RefCount( l.image:id ) }">
	<MvELSEIF EXPR = "{ GeneratedImage_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Increment_RefCount( l.image:image_id ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Update" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ LayoutValue_Load( l.layout_value:lc_id, l.layout_value:attr_id, l.prev_layout_value ) }">

	<MvIF EXPR = "{ l.prev_layout_value:value NE l.layout_value:value }">

		<MvIF EXPR = "{ NOT LayoutValue_Reference_Delete( l.prev_layout_value ) OR
						NOT LayoutValue_Reference_Insert( l.layout_value ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutValue_Reference_Delete" PARAMETERS = "layout_value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.layout_value:attr_id, l.attribute ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.attribute:type NE 'image' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Decrement_RefCount( l.image:id ) }">
	<MvELSEIF EXPR = "{ GeneratedImage_Load_File( l.layout_value:value, l.image ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Image_Decrement_RefCount( l.image:image_id ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "GeneratedImage_Load_File" PARAMETERS = "file, generatedimage var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "GeneratedImages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'GeneratedImages WHERE image = ?' }"
				FIELDS	= "l.file">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0140', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ GeneratedImages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0141' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].GeneratedImage_Read( l.generatedimage ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GeneratedImages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGLayoutCache
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutCache_Read" PARAMETERS = "layout_cache var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_cache:layout_id"	VALUE = "{ TGLayoutCache.d.layout_id  }">
	<MvASSIGN NAME = "l.layout_cache:value"		VALUE = "{ miva_array_deserialize( TGLayoutCache.d.value ) }">
	<MvASSIGN NAME = "l.layout_cache:dt_cached"	VALUE = "{ TGLayoutCache.d.dt_cached }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Insert" PARAMETERS = "layout_cache var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout_cache:value" VALUE = "{ miva_array_serialize( l.layout_cache:value ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGLayoutCache
						  ( layout_id, value, dt_cached )
						  VALUES
						  ( ?, ?, ? )' }"
			 FIELDS	= "l.layout_cache:layout_id, l.layout_cache:value, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0142', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Load" PARAMETERS = "layout_id, layout_cache var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache WHERE layout_id = ?' }"
				FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0143', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0144' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ LayoutCache_Read( l.layout_cache ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Delete" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache WHERE layout_id = ?' }"
			 FIELDS	= "l.layout_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0145', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Delete_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGLayoutCache' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0146', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Check_All" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{
								'SELECT
									layoutcache.layout_id
								FROM
									' $ g.Store_Table_Prefix $ 'TGLayoutCache layoutcache
								JOIN
									' $ g.Store_Table_Prefix $ 'TGLayoutComponents layoutcomponents
								ON
									layoutcache.layout_id = layoutcomponents.layout_id
								WHERE
									( 
										layoutcomponents.dt_start> 0 AND
										layoutcomponents.dt_start < ? AND
										layoutcomponents.dt_start> layoutcache.dt_cached
									)
									OR
									(
										layoutcomponents.dt_end> 0 AND
										layoutcomponents.dt_end < ? AND
										layoutcomponents.dt_end> layoutcache.dt_cached
									)' }"
				FIELDS	= "s.dyn_time_t, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0147', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0148' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutCache.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutCache_Delete( TGLayoutCache.d.layout_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutCache" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Check_Layout" PARAMETERS = "layout_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutCache"
				QUERY	= "{
								'SELECT
									layoutcache.layout_id
								FROM
									' $ g.Store_Table_Prefix $ 'TGLayoutCache layoutcache
								JOIN
									' $ g.Store_Table_Prefix $ 'TGLayoutComponents layoutcomponents
								ON
									layoutcache.layout_id = layoutcomponents.layout_id
								WHERE
									layoutcomponents.layout_id = ? AND
									( 
										layoutcomponents.dt_start> 0 AND
										layoutcomponents.dt_start < ? AND
										layoutcomponents.dt_start> layoutcache.dt_cached
									)
									OR
									(
										layoutcomponents.dt_end> 0 AND
										layoutcomponents.dt_end < ? AND
										layoutcomponents.dt_end> layoutcache.dt_cached
									)' }"
				FIELDS	= "l.layout_id, s.dyn_time_t, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0149', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGLayoutCache.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0150' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGLayoutCache.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutCache_Delete( TGLayoutCache.d.layout_id ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutCache" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutCache">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutCache_Recache" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.layout:layout_id" VALUE = "{ l.layout_id }">

	<MvIF EXPR = "{ NOT LayoutCache_Delete( l.layout:layout_id )					OR
					NOT Runtime_Layout_Load( l.layout:layout_id, l.layout:value )	OR
					NOT LayoutCache_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.layout:value" VALUE = "{ miva_array_deserialize( l.layout:value ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutCache_Load" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Check if Layout Cache needs to be deleted first.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT LayoutCache_Check_Layout( l.layout_id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Load( l.layout_id, l.layout ) }">
		<MvIF EXPR = "{ NOT LayoutCache_Recache( l.layout_id, l.layout ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	json functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "JSON_ComponentList_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.filter"			VALUE = "">
	<MvASSIGN NAME = "l.sort"			VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = 0>
	<MvASSIGN NAME = "l.count"			VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',			l.filter )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',			l.sort )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Offset',			l.offset,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Count',			l.count,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGComponents', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, l.filter,'id:s.id,code:s.code,name:s.name,descrip:s.descrip,image:s.image,alw_chldrn:s.alw_chldrn,disp_order:s.disp_order' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'id:s.id,code:s.code,name:s.name,descrip:s.descrip,image:s.image,alw_chldrn:s.alw_chldrn,disp_order:s.disp_order', 's.disp_order' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGComponents', l.search_sql, l.search_fields, l.offset, l.count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-UTL-CAL-0151', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.component_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGComponents.d.EOF ) AND ( ( l.count EQ 0 ) OR (l.component_count LT l.count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.component_count ) }">
					"id": <MvEVAL EXPR = "{ int( TGComponents.d.id ) }">,
					"code" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.code ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.name ) }">",
					"descrip" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.descrip ) }">",
					"image" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGComponents.d.image ) }">",
					"alw_chldrn" : <MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( TGComponents.d.alw_chldrn ) }">,
					"disp_order": <MvEVAL EXPR = "{ int( TGComponents.d.disp_order ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGComponents" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( l.offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponents">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',			l.component:code )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',			l.component:name )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Descrip',			l.component:descrip )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Image',			l.component:image )		OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Allow_Children',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ Component_Load_Code( l.component:code, l.existing_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Insert( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',				l.component:id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',			l.component:code )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',			l.component:name )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Descrip',			l.component:descrip )		OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Image',			l.component:image )			OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Allow_Children',	l.component:alw_chldrn ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:id, l.previous_component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ toupper( l.previous_component:code ) NE toupper( l.component:code ) }">
		<MvIF EXPR = "{ Component_Load_Code( l.component:code, l.existing_component ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'A component with the code \'' $ l.existing_component:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Update( l.component ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Component_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Components_DisplayOrder_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvFOREACH ITERATOR = "l.component" ARRAY = "g.Components_Order" INDEX = "l.pos">
		<MvASSIGN NAME = "l.component:id"				VALUE = "{ int( l.component:id ) }">
		<MvASSIGN NAME = "l.component:offset"			VALUE = "{ int( l.component:offset ) }">
		<MvASSIGN NAME = "l.component:original_offset"	VALUE = "{ int( l.component:original_offset ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.component:offset ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Components_Order[' $ l.pos $ ']', g.Validation_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvASSIGN NAME = "l.change_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.component" ARRAY = "g.Components_Order">
		<MvASSIGN NAME = "l.changes" INDEX = "{ ++l.change_count }"	VALUE = "{ l.component }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.change_count EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Components_Update_Offsets( l.changes ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_LayoutList_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "g.Filter"			VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort"			VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset"			VALUE = "{ int( g.Offset ) }">
	<MvASSIGN NAME = "g.Count"			VALUE = "{ int( g.Count ) }">
	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGLayouts', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'id:s.id,code:s.code,name:s.name' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'id:s.id,code:s.code,name:s.name', 's.id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGLayouts', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-UTL-CAL-0152', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGLayouts.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count )}">
					"id": <MvEVAL EXPR = "{ int( TGLayouts.d.id ) }">,
					"code" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGLayouts.d.code ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGLayouts.d.name ) }">"
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGLayouts" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayouts">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code( 'R', 'Code', l.layout:code ) OR
					NOT [ g.Module_JSON ].JSON_Input_Text( 'R', 'Name', l.layout:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',			l.layout:id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.layout:code )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',		l.layout:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Load_ID( l.layout:id, l.previous_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'Layout not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ toupper( l.previous_layout:code ) NE toupper( l.layout:code ) }">
		<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Update( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttributes_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.filter"			VALUE = "">
	<MvASSIGN NAME = "l.sort"			VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = 0>
	<MvASSIGN NAME = "l.count"			VALUE = 0>
	<MvASSIGN NAME = "g.cmpnt_id"		VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',			l.filter )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',			l.sort )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Offset',			l.offset,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Count',			l.count,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Component_ID',	g.cmpnt_id,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.search_query"	VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'attr.id				AS attr_id,
																				 attr.cmpnt_id			AS attr_cmpnt_id,
																				 attr.code				AS attr_code,
																				 attr.prompt			AS attr_prompt,
																				 attr.type				AS attr_type,
																				 attr.required			AS attr_required,
																				 attr.disp_order		AS attr_disp_order,
																				 opt.id					AS opt_id,
																				 opt.attr_id			AS opt_attr_id,
																				 opt.prompt				AS opt_prompt,
																				 opt.disp_order			AS opt_disp_order' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGComponentAttributes', 'attr' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.search_query, 'attr', g.Store_Table_Prefix $ 'TGComponentOptions', 'opt', 'opt.attr_id = attr.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'attr.cmpnt_id = ?', 'g.cmpnt_id' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, l.filter,'code:attr.code,prompt:attr.prompt;opt.prompt,type:attr.type,required:attr.required' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, l.sort,
																		'code:attr.code,
																		 prompt:attr.prompt;opt.prompt,
																		 type:attr.type,
																		 required:attr.required',
																		'attr.disp_order;opt.disp_order' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ l.search_sql }"
				FIELDS	= "{ l.search_fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-UTL-CAL-0153', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count"	VALUE = 0>
	<MvASSIGN NAME = "l.attr_count"		VALUE = 0>
	<MvASSIGN NAME = "l.opt_count"		VALUE = 0>
	<MvASSIGN NAME = "l.last_attr_id"	VALUE = 0>
	<MvASSIGN NAME = "l.last_opt_id"	VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"data":
		[
		<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
			<MvIF EXPR = "{ l.last_attr_id NE TGComponentAttributes.d.attr_id }">
				<MvIF EXPR = "{ l.opt_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					]
				</MvIF>

				<MvIF EXPR = "{ l.attr_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.attr_count ) }">
				"id":			<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_id ) }">,
				"cmpnt_id":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_cmpnt_id ) }">,
				"code":			"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_code ) }">",
				"prompt":		"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_prompt ) }">",
				"type":			"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.attr_type ) }">",
				"required":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_required ) }">,
				"disp_order":	<MvEVAL EXPR = "{ int( TGComponentAttributes.d.attr_disp_order ) }">

				<MvASSIGN NAME = "l.total_count"	VALUE = "{ l.total_count + 1 }">
				<MvASSIGN NAME = "l.last_attr_id"	VALUE = "{ TGComponentAttributes.d.attr_id }">
				<MvASSIGN NAME = "l.last_opt_id"	VALUE = 0>
				<MvASSIGN NAME = "l.opt_count"		VALUE = 0>
			</MvIF>

			<MvIF EXPR = "{ l.last_opt_id NE TGComponentAttributes.d.opt_id AND NOT ISNULL TGComponentAttributes.d.opt_id }">
				<MvIF EXPR = "{ l.opt_count }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
				</MvIF>

				<MvIF EXPR = "{ NOT l.opt_count }">
					, "options":
					[
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.opt_count ) }">
				"id":			<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_id ) }">,
				"attr_id":		<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_attr_id ) }">,
				"prompt":		"<MvEVAL EXPR = "{ encodejavascriptstring( TGComponentAttributes.d.opt_prompt ) }">",
				"disp_order":	<MvEVAL EXPR = "{ int( TGComponentAttributes.d.opt_disp_order ) }">

				<MvASSIGN NAME = "l.total_count" VALUE = "{ l.total_count + 1 }">
				<MvASSIGN NAME = "l.last_opt_id" VALUE = "{ TGComponentAttributes.d.opt_id }">
			</MvIF>

			<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

		<MvIF EXPR = "{ l.opt_count }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
			]
		</MvIF>

		<MvIF EXPR = "{ l.attr_count }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
		</MvIF>
		],
		"start_offset": 0,
		"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">
	}
}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Component_ID',	l.component_attribute:cmpnt_id,	-1, -1 )												OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',			l.component_attribute:code )															OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',			l.component_attribute:prompt )															OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'R', 'Type',			l.component_attribute:type,	l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 'Required',		l.component_attribute:required ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component_attribute:cmpnt_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Component_ID', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ ComponentAttribute_Load_Code( l.component_attribute:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Insert( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvASSIGN NAME = "l.component_attribute_types" VALUE = "{ ComponentAttribute_Types() }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',				l.component_attribute:id,	-1, -1 )													OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Component_ID',	l.component_attribute:cmpnt_id,	-1, -1 )												OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',			l.component_attribute:code )															OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',			l.component_attribute:prompt )															OR
					NOT [ g.Module_JSON ].JSON_Input_List(		'R', 'Type',			l.component_attribute:type,	l.component_attribute_types, l.component_attribute_types )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'O', 'Required',		l.component_attribute:required ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_attribute:id, l.previous_component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component_attribute:cmpnt_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Component_ID', 'Component not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ toupper( l.component_attribute:code ) NE toupper( l.previous_component_attribute:code ) }">
		<MvIF EXPR = "{ ComponentAttribute_Load_Code( l.component_attribute:cmpnt_id, l.component_attribute:code, l.existing_component_attribute ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'An attribute with the code \'' $ l.existing_component_attribute:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Update( l.component_attribute ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttribute_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentAttributes_DisplayOrder_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">															<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">												<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">							<MvFUNCTIONRETURN> </MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'r', 'Component_ID', l.cmpnt_id, -1, -1 ) }">	<MvFUNCTIONRETURN> </MvIF>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Components_Order" INDEX = "l.pos">
		<MvASSIGN NAME = "l.attributes_order:id"				VALUE = "{ int( l.attributes_order:id ) }">
		<MvASSIGN NAME = "l.attributes_order:offset"			VALUE = "{ int( l.attributes_order:offset ) }">
		<MvASSIGN NAME = "l.attributes_order:original_offset"	VALUE = "{ int( l.attributes_order:original_offset ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.attributes_order:offset ) }">
			<MvFUNCTIONRETURN VALUE VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Components_Order[' $ l.pos $ ']', g.Validation_Message ) }">
		</MvIF>
	</MvFOREACH>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Option_Order" INDEX = "l.attr_pos">
		<MvFOREACH ITERATOR = "l.option_order" ARRAY = "l.attributes_order" INDEX = "l.option_pos">
			<MvASSIGN NAME = "l.option_order:id"				VALUE = "{ int( l.option_order:id ) }">
			<MvASSIGN NAME = "l.option_order:offset"			VALUE = "{ int( l.option_order:offset ) }">
			<MvASSIGN NAME = "l.option_order:original_offset"	VALUE = "{ int( l.option_order:original_offset ) }">

			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.option_order:offset ) }">
				<MvFUNCTIONRETURN VALUE VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Option_Order[' $ l.attr_pos $ '][ ' $ l.option_pos $ ' ]', g.Validation_Message ) }">
			</MvIF>
		</MvFOREACH>
	</MvFOREACH>

	<MvASSIGN NAME = "l.field_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Components_Order">
		<MvIF EXPR = "{ l.attributes_order:offset EQ l.attributes_order:original_offset }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.attr_changes" INDEX = "{ ++l.field_count }" VALUE = "{ l.attributes_order }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.field_count }">
		<MvIF EXPR = "{ NOT ComponentAttribute_Update_Offsets( l.cmpnt_id, l.attr_changes ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.attributes_order" ARRAY = "g.Option_Order" INDEX = "l.attr_id">
		<MvASSIGN NAME = "l.option_count" VALUE = 0>

		<MvFOREACH ITERATOR = "l.option_order" ARRAY = "l.attributes_order" INDEX = "l.option_pos">
			<MvIF EXPR = "{ l.option_order:offset EQ l.option_order:original_offset }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.option_changes" INDEX = "{ ++l.option_count }" VALUE = "{ l.option_order }">
		</MvFOREACH>

		<MvIF EXPR = "{ l.option_count }">
			<MvIF EXPR = "{ NOT ComponentOptions_Update_Offsets( l.attr_id, l.option_changes ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Attribute_ID',	l.component_option:attr_id,	-1, -1 ) OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',			l.component_option:prompt ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_option:attr_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Attribute_ID', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Insert( l.component_option ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',				l.component_option:id,	-1, -1 )		OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Attribute_ID',	l.component_option:attr_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Prompt',			l.component_option:prompt ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentAttribute_Load_ID( l.component_option:attr_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Attribute_ID', 'Attribute not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Update( l.component_option ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentOption_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID', l.id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT ComponentOption_Delete( l.id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_LayoutComponents_Load_Layout" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Layout_ID', l.layout_id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.layout_components_count" VALUE = "{ LayoutComponents_Load_Parent( l.layout_id, 0, l.layout_components ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Output( l.layout_components ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ComponentList_Load_All" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ ComponentList_Load_All( l.components ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Output( l.components ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "[]">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Save" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'Layout_ID', l.layout_id, -1, -1 ) OR
					NOT [ g.Module_JSON ].JSON_Input_Retrieve_Raw( 'Layout_Data', l.layout_data ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT LayoutComponents_Save( l.layout_id, l.layout_data:layout, l.layout_data:deleted ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Layout_Duplicate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'Layout_ID',	l.duplicate_layout_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Code(		'R', 'Code',		l.layout:code )						OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'R', 'Name',		l.layout:name )}">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ Layout_Load_Code( l.layout:code, l.existing_layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'A layout with the code \'' $ l.existing_layout:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Insert( l.layout ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Layout_Duplicate( l.layout:id, l.duplicate_layout_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_LayoutCache_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">									<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">						<MvFUNCTIONRETURN>	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'TGCOMPONENTS', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN>	</MvIF>

	<MvIF EXPR = "{ NOT LayoutCache_Delete_All() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvCOMMENT>
|
|	Custom functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "LayoutComponents_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ LayoutComponent_Read( l.component ) }">

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:cmpnt_id, l.component:component ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.component:cmpnt_id, l.component:component:attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{	NOT LayoutComponent_Load_Values( l.component:id, l.component:component:attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Read" PARAMETERS = "component var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ LayoutComponent_Read( l.component ) }">

	<MvIF EXPR = "{ NOT Component_Load_ID( l.component:cmpnt_id, l.component:component ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{	NOT ComponentAttributes_Load_All( l.component:cmpnt_id, l.attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{	NOT LayoutComponent_Load_Values( l.component:id, l.attributes ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes">
		<MvREFERENCEARRAY NAME = "l.component_attribute" VARIABLE = "l.component:attributes">
			<MvMEMBER NAME = "{ l.attribute:code }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.component_attribute:value"			VALUE = "{ l.attribute:value }">

		<MvIF EXPR = "{ l.attribute:link }">
			<MvASSIGN NAME = "l.component_attribute:link"		VALUE = "{ l.attribute:link }">
			<MvASSIGN NAME = "l.component_attribute:value"		VALUE = "{ Load_URI( l.attribute:link ) }">
		<MvELSEIF EXPR = "{ l.attribute:category }">
			<MvASSIGN NAME = "l.component_attribute:category"	VALUE = "{ l.attribute:category }">
		<MvELSEIF EXPR = "{ l.attribute:product }">
			<MvASSIGN NAME = "l.component_attribute:product"	VALUE = "{ l.attribute:product }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentAttributes_Load_All" PARAMETERS = "cmpnt_id, component_attributes var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGComponentAttributes"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGComponentAttributes WHERE cmpnt_id = ? ORDER BY disp_order' }"
				FIELDS	= "l.cmpnt_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0154', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGComponentAttributes.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-UTL-CAL-0155' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGComponentAttributes.d.EOF }">
		<MvEVAL EXPR = "{ ComponentAttribute_Read( l.component_attributes[ ++l.count ] ) }">

		<MvIF EXPR = "{ l.component_attributes[ l.count ]:type EQ 'select' OR l.component_attributes[ l.count ]:type EQ 'radio' }">
			<MvASSIGN NAME ="l.null" VALUE = "{ ComponentOptions_Load_Attribute( l.component_attributes[ l.count ]:id, l.component_attributes[ l.count ]:options ) }">
		<MvELSEIF EXPR = "{ l.component_attributes[ l.count ]:type EQ 'imagetype' }">
			<MvIF EXPR = "{ ISNULL g.Session:cache:tgcomponents:imagetypes }">
				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( g.Session:cache:tgcomponents:imagetypes )  }">
			</MvIF>

			<MvASSIGN NAME = "l.component_attributes" INDEX = "{ l.count }" MEMBER = "options" VALUE = "{ g.Session:cache:tgcomponents:imagetypes }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGComponentAttributes" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGComponentAttributes">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponent_Load_Values" PARAMETERS = "lc_id, attributes var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.attributes">
		<MvIF EXPR = "{ NOT LayoutValue_Load( l.lc_id, l.attribute:id, l.layout_value ) }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "">
			<MvFOREACHCONTINUE>
		<MvELSEIF EXPR = "{ ISNULL l.layout_value:value }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.attribute:value" VALUE = "{ l.layout_value:value }">

		<MvIF EXPR = "{ l.attribute:type EQ 'link' }">
			<MvASSIGN NAME = "l.attribute:link" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:link:type }">		<MvASSIGN NAME = "l.attribute:link:type"		VALUE = "">	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:link:value }">		<MvASSIGN NAME = "l.attribute:link:value"		VALUE = "">	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:link:targ }">		<MvASSIGN NAME = "l.attribute:link:targ"		VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'product' }">
			<MvASSIGN NAME = "l.attribute:product" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:product:id }">		<MvASSIGN NAME = "l.attribute:product:id"		VALUE = 0>	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:product:code }">		<MvASSIGN NAME = "l.attribute:product:code"		VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'category' }">
			<MvASSIGN NAME = "l.attribute:category" VALUE = "{ miva_array_deserialize( l.attribute:value ) }">

			<MvIF EXPR = "{ ISNULL l.attribute:category:id }">		<MvASSIGN NAME = "l.attribute:category:id"		VALUE = 0>	</MvIF>
			<MvIF EXPR = "{ ISNULL l.attribute:category:code }">	<MvASSIGN NAME = "l.attribute:category:code"	VALUE = "">	</MvIF>
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'multitext' }">
			<MvASSIGN NAME = "l.null" VALUE = "{ miva_splitstring( l.attribute:value, '|', l.attribute:value, 'trim' ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Load_Parent_LowLevel" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND parent = ? ORDER BY disp_order' }"
				FIELDS	= "l.layout_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0156', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvIF EXPR = "{ NOT LayoutComponents_Read( l.layout_components[ ++l.count ] ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0157', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Load_Parent" PARAMETERS = "layout_id, parent_id, components var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.components_count" VALUE = "{ LayoutComponents_Load_Parent_LowLevel( l.layout_id, l.parent_id, l.components ) }">

	<MvFOREACH ITERATOR = "l.component" ARRAY = "l.components" COUNT = "{ l.components_count }">
		<MvIF EXPR = "{ l.component:component:alw_chldrn EQ 1 }">
			<MvASSIGN NAME = "l.component:children_count" VALUE = "{ LayoutComponents_Load_Parent( l.component:layout_id, l.component:id, l.component:children ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.components_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save" PARAMETERS = "layout_id, layout_components var, deleted_components var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.disp_order" VALUE = 0>

	<MvIF EXPR = "{ NOT LayoutComponents_Save_LowLevel( l.layout_id, l.layout_components, l.disp_order )	OR
					NOT LayoutComponents_Delete_Components( l.deleted_components )							OR
					NOT LayoutCache_Recache( l.layout_id, l.null ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Load_Parent_LowLevel" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGLayoutComponents"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGLayoutComponents WHERE layout_id = ? AND parent = ? AND active = 1 ORDER BY disp_order' }"
				FIELDS	= "l.layout_id, l.parent_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-UTL-CAL-0158', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGLayoutComponents.d.EOF }">
		<MvIF EXPR = "{ Runtime_LayoutComponent_Schedule_Active( TGLayoutComponents.d.dt_start, TGLayoutComponents.d.dt_end ) }">
			<MvIF EXPR = "{ NOT Runtime_LayoutComponents_Read( l.layout_components[ ++l.count ] ) }">
				<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
					<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>
			</MvIF>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGLayoutComponents" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGLayoutComponents">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-UTL-CAL-0159', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponents_Load_Parent" PARAMETERS = "layout_id, parent_id, layout_components var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.components_count" VALUE = "{ Runtime_LayoutComponents_Load_Parent_LowLevel( l.layout_id, l.parent_id, l.layout_components ) }">

	<MvFOREACH ITERATOR = "l.component" ARRAY = "l.layout_components" COUNT = "{ l.components_count }">
		<MvIF EXPR = "{ l.component:component:alw_chldrn EQ 1 }">
			<MvASSIGN NAME = "l.component:children_count" VALUE = "{ Runtime_LayoutComponents_Load_Parent( l.layout_id, l.component:id, l.component:children ) }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.components_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_LayoutComponent_Schedule_Active" PARAMETERS = "dt_start, dt_end" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.dt_start GT 0 ) AND ( l.dt_start GT s.dyn_time_t ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ( l.dt_end GT 0 ) AND ( l.dt_end LE s.dyn_time_t ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_Layout_Load" PARAMETERS = "layout_id, layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Load_ID( l.layout_id, l.loaded_layout ) OR
					NOT Runtime_LayoutComponents_Load_Parent( l.layout_id, 0, l.layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save_LowLevel" PARAMETERS = "layout_id, layout_components var, disp_order var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Recursive Function that will update the layout components.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.disp_order }"> <MvASSIGN NAME = "l.disp_order" VALUE = 0> </MvIF>

	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.layout_components:children">
		<MvASSIGN NAME = "l.layout_component:layout_id"		VALUE = "{ l.layout_id }">
		<MvASSIGN NAME = "l.layout_component:id"			VALUE = "{ int( l.layout_component:id ) }">
		<MvASSIGN NAME = "l.layout_component:parent"		VALUE = "{ int( l.layout_components:id ) }">
		<MvASSIGN NAME = "l.layout_component:cmpnt_id"		VALUE = "{ int( l.layout_component:component:id ) }">
		<MvASSIGN NAME = "l.layout_component:disp_order"	VALUE = "{ ++l.disp_order }">

		<MvIF EXPR = "{ l.layout_component:id EQ 0 }">
			<MvIF EXPR = "{ NOT LayoutComponent_Insert( l.layout_component ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		<MvELSEIF EXPR = "{ NOT LayoutComponent_Update( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_Attributes( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Save_LowLevel( l.layout_id, l.layout_component, l.disp_order ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Save_Attributes" PARAMETERS = "layout_component var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.attribute" ARRAY = "l.layout_component:component:attributes">
		<MvASSIGN NAME = "l.attribute:lc_id"	VALUE = "{ l.layout_component:id }">
		<MvASSIGN NAME = "l.attribute:attr_id"	VALUE = "{ l.attribute:id }">
		<MvASSIGN NAME = "l.attribute:value"	VALUE = "{ l.attribute:value }">

		<MvIF EXPR = "{ l.attribute:type EQ 'link' }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:link ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'product' }">
			<MvASSIGN NAME = "l.attribute:product:id" VALUE = 0>

			<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_Code( l.attribute:product:code, l.loaded_product ) }">
				<MvASSIGN NAME = "l.attribute:product:id"	VALUE = "{ l.loaded_product:id }">
				<MvASSIGN NAME = "l.attribute:product:code"	VALUE = "{ l.loaded_product:code }">
			</MvIF>

			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:product ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'category' }">
			<MvASSIGN NAME = "l.attribute:category:id" VALUE = 0>

			<MvIF EXPR = "{ [ g.Module_Library_DB ].Category_Load_Code( l.attribute:category:code, l.loaded_category ) }">
				<MvASSIGN NAME = "l.attribute:category:id"		VALUE = "{ l.loaded_category:id }">
				<MvASSIGN NAME = "l.attribute:category:code"	VALUE = "{ l.loaded_category:code }">
			</MvIF>

			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_array_serialize( l.attribute:category ) }">
		<MvELSEIF EXPR = "{ l.attribute:type EQ 'multitext' }">
			<MvASSIGN NAME = "l.attribute:value" VALUE = "{ miva_joinstring( l.attribute:value, asciichar( 10 ), 'trim' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutValue_Insert( l.attribute ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT LayoutValue_Update( l.attribute ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "LayoutComponents_Delete_Components" PARAMETERS = "deleted_components var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.layout_component" ARRAY = "l.deleted_components:children">
		<MvASSIGN NAME = "l.layout_component:id" VALUE = "{ int( l.layout_component:id ) }">

		<MvIF EXPR = "{ l.layout_component:id GT 0 }">
			<MvIF EXPR = "{ NOT LayoutComponent_Delete( l.layout_component:id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT LayoutComponents_Delete_Components( l.layout_component ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Layout_Load_Code" PARAMETERS = "module var, param, layout_code, layout var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Layout_Load_Code( l.layout_code, l.loaded_layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Runtime_LayoutCache_Load( l.loaded_layout:id, l.loaded_layout ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.layout" VALUE = "{ l.loaded_layout:value }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_URI" PARAMETERS = "link var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.flags:nosession" VALUE = 1>

	<MvIF EXPR = "{ l.link:type EQ 'P' AND l.link:value }">		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'C' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Category_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'G' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Page_Code_URL( l.link:value, l.flags ) }">
	<MvELSEIF EXPR = "{ l.link:type EQ 'U' AND l.link:value }">	<MvFUNCTIONRETURN VALUE = "{ l.link:value }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvCOMMENT>
|
|	Helper component functions
|
</MvCOMMENT>

<MvFUNCTION NAME = "Parse_Function_Parameters" PARAMETERS = "string, function_name var, parameters var, parameter_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.parameter_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.loop_counter" 		VALUE = 0>
	<MvASSIGN NAME = "l.string" 			VALUE = "{ trim( l.string ) }">
	<MvASSIGN NAME = "l.starting_pos" 		VALUE = "{ indexof( '(', l.string, 1 ) + 1 }">
	<MvASSIGN NAME = "l.function_name" 		VALUE = "{ tolower( substring_var( l.string, 1, l.starting_pos - 2 ) ) }">

	<MvIF EXPR = "{ l.starting_pos EQ 1 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.function_name }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ substring_var( l.string, len_var( l.string ), 1 ) NE ')' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvWHILE EXPR = "{ 1 }">
		<MvASSIGN NAME = "l.quote_pos" 		VALUE = "{ indexof( '\'', l.string, l.starting_pos ) }">
		<MvASSIGN NAME = "l.end_quote_pos" 	VALUE = "{ indexof( '\'', l.string, l.quote_pos + 1 ) }">
		<MvASSIGN NAME = "l.comma_pos" 		VALUE = "{ indexof( ',', l.string, l.starting_pos ) }">

		<MvIF EXPR = "{ l.quote_pos AND NOT l.end_quote_pos }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvWHILE EXPR = "{ ( l.comma_pos LT l.end_quote_pos ) AND ( l.comma_pos GT l.quote_pos ) }">
			<MvASSIGN NAME = "l.comma_pos" VALUE = "{ indexof( ',', l.string, l.comma_pos + 1 ) }">
		</MvWHILE>

		<MvIF EXPR = "{ l.comma_pos EQ 0 }">
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ len_var( l.string ) - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ len_var( l.string ) }">
			<MvASSIGN NAME = "l.quote_pos" 		VALUE = 0>
		<MvELSE>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ l.comma_pos - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ l.comma_pos + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.param" VALUE = "{ trim( substring_var( l.string, l.start, l.end ) ) }">

		<MvIF EXPR = "{ ISNULL l.param }">
			<MvIF EXPR = "{ l.comma_pos EQ 0 AND l.parameter_count EQ 0 }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.parameters" INDEX = "{ ++l.parameter_count }" VALUE = "{ l.param }">

		<MvIF EXPR = "{ l.quote_pos EQ 0 AND l.comma_pos EQ 0 }">
			<MvWHILESTOP>
		</MvIF>

		<MvIF EXPR = "{ l.loop_counter GT 1000 }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.loop_counter" VALUE = "{ l.loop_counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Is_Variable" PARAMETERS = "variable var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.valid_chars" 		VALUE = "_.: ">
	<MvASSIGN NAME = "l.square_bracket_pos" VALUE = 0>
	<MvASSIGN NAME = "l.bracket_count" 		VALUE = 0>

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.variable ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.variable, l.pos, 1 ) }">

		<MvIF EXPR = "{ l.pos EQ 1 }">
			<MvIF EXPR = "{ ( NOT isalpha( l.char ) ) AND
							( l.char NE '_' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFORCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.char EQ '[' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count + 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos + 1 ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ ']' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count - 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos + 1 ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( NOT isdigit( l.char ) ) AND
							( NOT isalpha( l.char ) ) AND
							( NOT ( l.char IN l.valid_chars ) ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ l.bracket_count NE 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>
